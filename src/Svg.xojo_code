#tag Module
Protected Module SVG
	#tag Method, Flags = &h21
		Private Function angleBetweenVectors(ux As Double, uy As Double, vx As Double, vy As Double) As Double
		  // Angle of vector V from the positive X-axis
		  Var angleV As Double
		  angleV = Atan2(vy, vx) * (180 / Acos(-1)) // Convert Radians to Degrees
		  
		  // Angle of vector U from the positive X-axis
		  Var angleU As Double
		  angleU = Atan2(uy, ux) * (180 / Acos(-1)) // Convert Radians to Degrees
		  
		  // Calculate the difference
		  Var angleDelta As Double = angleV - angleU
		  
		  // Normalize the angle to be within [0, 360) degrees
		  // Use Modulo operator for normalization
		  angleDelta = angleDelta Mod 360
		  
		  // If the result is negative, normalize it back to positive [0, 360)
		  if angleDelta < 0 then
		    angleDelta = angleDelta + 360
		  end if
		  
		  Return angleDelta
		  
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub ApplyValues(Extends Item As JSONItem, withItem As JSONItem)
		  Var i As Integer
		  
		  i = 0
		  while i < withItem.Count
		    Item.Value(withItem.KeyAt(i)) = withItem.Value(withItem.KeyAt(i))
		    i = i + 1
		  wend
		  
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub BuildNodeDictionary(node As XMLNode)
		  Var nodeId As String
		  Var i As Integer
		  
		  nodeId = node.GetAttribute("id")
		  if nodeId <> "" then
		    if not mNodes.HasKey(nodeId) then
		      mNodes.Value("#" + nodeId) = node
		    end if
		  end if
		  
		  i = 0 
		  while i < node.ChildCount
		    if (not node.Child(i) IsA XMLTextNode) and _
		      (not node.Child(i) IsA XMLComment) and _
		      (not node.Child(i) IsA XMLProcessingInstruction) then
		      BuildNodeDictionary(node.Child(i))
		    end if
		    i = i + 1
		  wend
		  
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Function buildStyleItem(node As XmlNode) As JSONItem
		  Var result as new JSONItem("{}")
		  Var i As Integer
		  Var j As Integer
		  Var xAttr As XmlAttribute
		  Var styleArr() As String
		  Var itemArr() As String
		  Var className As String
		  Var classProperties As JSONItem
		  
		  if mClasses.HasKey(node.Name.Lowercase) then
		    classProperties = mClasses.Value(node.Name.Lowercase)
		    result.ApplyValues classProperties
		  end if
		  
		  i = 0
		  while i < node.AttributeCount
		    xAttr = node.GetAttributeNode(i)
		    
		    if xAttr.Name = "class" then
		      
		      className = node.GetAttribute(xAttr.Name).Lowercase().Trim()
		      if mClasses.HasKey("." + className) then
		        classProperties = mClasses.Value("." + className)
		        result.ApplyValues classProperties
		      end if
		      
		    elseif xAttr.Name = "style" then
		      
		      // process style attribute
		      
		      styleArr = node.GetAttribute(xAttr.Name).Split(";")
		      j = 0
		      while j <= styleArr.LastIndex
		        itemArr = styleArr(j).Split(":")
		        if itemArr.LastIndex = 1 then
		          result.Value(itemArr(0).Trim.Lowercase) = itemArr(1)
		        end if
		        j = j + 1
		      wend
		      
		    elseif xAttr.Name.IndexOf(":") < 0 then
		      
		      result.Value(xAttr.Name.Lowercase) = node.GetAttribute(xAttr.Name)
		      
		    end if
		    
		    i = i + 1
		  wend
		  
		  return result
		  
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Function buildTransformationMatrix(transform As String) As Double()
		  Var result() As Double = Array( _
		  1.0, 0.0, 0.0, _
		  0.0, 1.0, 0.0, _
		  0.0, 0.0, 1.0)
		  Var mulMatrix() As Double = Array( _
		  0.0, 0.0, 0.0, _
		  0.0, 0.0, 0.0, _
		  0.0, 0.0, 0.0)
		  Var pos As Integer
		  Var openBracket As Integer
		  Var closeBracket As Integer
		  Var functionName As String
		  Var parms As String
		  Var strArr() As String
		  
		  pos = 0
		  
		  do
		    //pos = pos + 1
		    //openBracket = Instr(pos, transform, "(")
		    openBracket = transform.IndexOf(pos, "(")
		    //if openBracket > 0 then
		    if openBracket >= 0 then
		      
		      //closeBracket = Instr(openBracket, transform, ")")
		      closeBracket = transform.IndexOf(openBracket, ")")
		      if closeBracket > 0 then
		        
		        //functionName = Lowercase(Trim(Mid(transform, pos, openBracket - pos)))
		        functionName = transform.Middle(pos, openBracket - pos).Trim().Lowercase()
		        //parms = Mid(transform, openBracket + 1, closeBracket - openBracket - 1)
		        parms = transform.Middle(openBracket + 1, closeBracket - openBracket - 1)
		        parms = parms.ReplaceAll(",", " ")
		        parms = collapseSpaces(parms)
		        strArr = parms.Split(" ")
		        
		        select case functionName
		          
		        case "matrix"
		          if strArr.LastIndex = 5 then
		            mulMatrix = matrix(val(strArr(0)), _ ' a
		            val(strArr(1)), _ ' b
		            val(strArr(2)), _ ' c
		            val(strArr(3)), _ ' d
		            val(strArr(4)), _ ' e
		            val(strArr(5)) ) ' f
		            result = matrixMultiply(result, mulMatrix)
		          end if
		          
		        case "rotate"
		          if strArr.LastIndex = 0 then // around origin
		            mulMatrix = rotationMatrix(val(strArr(0)))
		            result = matrixMultiply(result, mulMatrix)
		          elseif strArr.LastIndex = 2 then // around point
		            mulMatrix = translationMatrix(val(strArr(1)), val(strArr(2)))
		            result = matrixMultiply(result, mulMatrix)
		            mulMatrix = rotationMatrix(val(strArr(0)))
		            result = matrixMultiply(result, mulMatrix)
		            mulMatrix = translationMatrix(-val(strArr(1)), -val(strArr(2)))
		            result = matrixMultiply(result, mulMatrix)
		          end if
		          
		        case "scale"
		          if strArr.LastIndex >= 1 then
		            mulMatrix = scaleMatrix(val(strArr(0)), val(strArr(1)))
		          else
		            mulMatrix = scaleMatrix(val(strArr(0)), val(strArr(0)))
		          end if
		          result = matrixMultiply(result, mulMatrix)
		          
		        case "skewx"
		          if strArr.LastIndex >= 0 then
		            mulMatrix = skewXMatrix(val(strArr(0)))
		            result = matrixMultiply(result, mulMatrix)
		          end if
		          
		        case "skewy"
		          if strArr.LastIndex >= 0 then
		            mulMatrix = skewYMatrix(val(strArr(0)))
		            result = matrixMultiply(result, mulMatrix)
		          end if
		          
		        case "translate"
		          if strArr.LastIndex >= 1 then
		            mulMatrix = translationMatrix(val(strArr(0)), val(strArr(1)))
		          else
		            mulMatrix = translationMatrix(val(strArr(0)), 0)
		          end if
		          result = matrixMultiply(result, mulMatrix)
		          
		        end select
		        
		        pos = closeBracket
		      else
		        pos = -1
		      end if
		      
		    else
		      pos = -1
		    end if
		    
		    pos = pos + 1
		    
		  loop until (pos >= transform.Length) or (pos < 0)
		  
		  
		  return result
		  
		  
		  
		  
		  'return resultMatrix
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Function collapseSpaces(value As String) As String
		  Var result As String
		  
		  result = value
		  while result.IndexOf("  ") >= 0
		    result = result.ReplaceAll("  ", " ")
		  wend
		  
		  return result
		  
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Function colorFromHex(s As String) As Color
		  Var result As Color
		  Var colVariant As Variant
		  Var tmpStr As String
		  
		  if s.Left(1) = "#" then
		    tmpStr = s.Right(s.Length - 1)
		  else
		    tmpStr = s
		  end if
		  
		  if tmpStr.Length = 3 then
		    tmpStr = tmpStr.Left(1) + tmpStr.Left(1) + tmpStr.Middle(1, 1) + tmpStr.Middle(1, 1) + tmpStr.Right(1) + tmpStr.Right(1)
		  end if
		  
		  tmpStr = "&c" + tmpStr
		  
		  colVariant = tmpStr
		  result = colVariant.ColorValue
		  
		  return result
		  
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Function determineColor(s As String) As Color
		  Var col As Color
		  Var colStr As String
		  Var startPos As Integer
		  Var endPos As Integer
		  Var tmpStr As String
		  Var tmpArr() As String
		  
		  Static ColorTable As new Dictionary("aliceblue" : &cf0f8ff, "antiquewhite" : &cfaebd7, "aqua" : &c00ffff, "azure" : &c0fffff,  _
		  "beige": &cf5f5dc, "bisque" : &cffe4c4, "black" : &c000000, "blanchedalmond" : &cffebcd, "blue" : &c0000ff, _
		  "blueviolet" : &c8a2be2, "brown" : &ca52a2a, "burlywood" : &cdeb887, "cadetblue" : &c5f9ea0, "chartreuse" : &c7fff00, _
		  "chocolate" : &cd2691e, "coral" : &cff7f50, "cornflowerblue" : &c6495ed, "cornsilk" : &cfff8dc, "crimson" : &cdc143c, _
		  "cyan" : &c00ffff, "darkblue" : &c00008b, "darkcyan" : &c008b8b, "darkgoldenrod" : &cb8860b, "darkgray" : &ca9a9a9, _
		  "darkgreen" : &c006400, "darkgrey" : &ca9a9a9, "darkkhaki" : &cbdb76b, "darkmagenta" : &c8b008b, "darkolivegreen" : &c556b2f, _
		  "darkorange" : &cff8c00, "darkorchid" : &c9932cc, "darkred" : &c8b0000, "darksalmon" : &c39967a, "darkseagreen" : &c8fbc8f, _
		  "darkslateblue" : &c483d8b, "darkslategray" : &c2f4f4f, "darkslategrey" : &c2f4f4f, "darkturquoise" : &c00ced1, _
		  "darkviolet" : &c9400d3, "deeppink" : &cff1493, "deepskyblue" : &c00bfff, "dimgray" : &c696969, "dimgrey" : &c696969, _
		  "dodgerblue" : &c1e90ff, "firebrick" : &cb22222, "floralwhite" : &cfffaf0, "forestgreen" : &c228b22, "fuchsia" : &cff00ff, _
		  "gainsboro" : &cdcdcdc, "ghostwhite" : &cf8f8ff, "gold" : &cffd700, "goldenrod" : &cdaa520, "gray" : &c8080, "grey" : &c8080, _
		  "green" : &c008000, "greenyellow" : &cadff2f, "honeydew" : &cf0fff0, "hotpink" : &cff69b4, "indianred" : &ccd5c5c, _
		  "indigo" : &c4b0082, "ivory" : &cfffff0, "khaki" : &cf0e68c, "lavender" : &ce6e6fa, "lavenderblush" : &cfff0f5, _
		  "lawngreen" : &c7cfc00, "lemonchiffon" : &cfffacd, "lightblue" : &cadd8e6, "lightcoral" : &cf08080, "lightcyan" : &ce0ffff, _
		  "lightgoldenrodyellow" : &cfafad2, "lightgray" : &cd3d3d3, "lightgreen" : &c90ee90, "lightgrey" : &cd3d3d3, _
		  "lightpink" : &cffb6c1, "lightsalmon" : &cffa07a, "lightseagreen" : &c20b2aa, "lightskyblue" : &c87cefa, "lightslategray" : &c778899, _
		  "lightslategrey" : &c778899, "lightsteelblue" : &cb0c4de, "lightyellow" : &cffffe0, "lime" : &c00ff00, "limegreen" : &c32cd32, _
		  "linen" : &cfaf0e6, "magenta" : &cff00ff, "maroon" : &c800000, "mediumaquamarine" : &c66cdaa, "mediumblue" : &c0000cd, _
		  "mediumorchid" : &cba55d3, "mediumpurple" : &c9370db, "mediumseagreen" : &c3cb371, "mediumslateblue" : &c7b68ee, _
		  "mediumspringgreen" : &c00fa9a, "mediumturquoise" : &c48d1cc, "mediumvioletred" : &cc71585, "midnightblue" : &c191970, _
		  "mintcream" : &cf5fffa, "mistyrose" : &cffe4e1, "moccasin" : &cffe4b5, "navajowhite" : &cffdead, "navy" : &c000080, _
		  "oldlace" : &cfdf5e6, "olive" : &c808000, "olivedrab" : &c6b8e23, "orange" : &cffa500, "orangered" : &cff4500, "orchid" : &cda70d6, _
		  "palegoldenrod" : &ceee8aa, "palegreen" : &c98fb98, "paleturquoise" : &cafeeee, "palevioletred" : &cdb7093, _
		  "papayawhip" : &cffefd5, "peachpuff" : &cffdab9, "peru" : &ccd853f, "pink" : &cffc0cb, "plum" : &cdda0dd, _
		  "powderblue" : &cb0e0e6, "purple" : &c800080, "red" : &cff0000, "rosybrown" : &cbc8f8f, "royalblue" : &c4169e1, _
		  "saddlebrown" : &c8b4513, "salmon" : &cfa8072, "sandybrown" : &cf4a460, "seagreen" : &c2e8b57, "seashell" : &cfff5ee, _
		  "sienna" : &ca0522d, "silver" : &cc0c0c0, "skyblue" : &c87ceeb, "slateblue" : &c6a5acd, "slategray" : &c708090, _
		  "slategrey" : &c708090, "snow" : &cfffafa, "springgreen" : &c00ff7f, "steelblue" : &c4682b4, "tan" : &cd2b4bc, "teal" : &c008080, _
		  "thistle" : &cd8bfd8, "tomato" : &cff6347, "turquoise" : &c40e0d0, "violet" : &cee82ee, "wheat" : &cf5deb3, "white" : &cffffff, _
		  "whitesmoke" : &cf5f5f5, "yellow" : &cffff00, "yellowgreen" : &c9acd32)
		  
		  colStr = s.Trim().Lowercase()
		  
		  if ColorTable.HasKey(colStr) then
		    col = ColorTable.Value(colStr)
		  elseif colStr.Left(3) = "rgb" then
		    startPos = colStr.IndexOf(0, "(")
		    endPos = colStr.IndexOf(startPos + 1, ")")
		    if (startPos >= 0) and (endPos >= 0) then
		      tmpStr = colStr.Middle(startPos + 1, endPos - startPos - 1)
		      tmpArr = tmpStr.Split(",")
		      if tmpArr.LastIndex = 2 then
		        col = Color.RGB(Val(tmpArr(0)), Val(tmpArr(1)), Val(tmpArr(2)))
		      end if
		    end if
		  else
		    col = colorFromHex(colStr)
		  end if
		  
		  return col
		  
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Function DetermineStopColor(style As String) As Color
		  Var stopColor As Color
		  Var styleArr() As String
		  Var i As Integer
		  Var fillOpacity As Double
		  Var baseColor As Color
		  
		  fillOpacity = 1.0
		  styleArr = style.ReplaceAll(" ", "").Split(";")
		  i = 0
		  while i < styleArr.Count
		    if styleArr(i).Left(11) = "stop-color:" then
		      baseColor = determineColor(styleArr(i).Right(styleArr(i).Length - 11))
		    elseif styleArr(i).Left(13) = "stop-opacity:" then
		      fillOpacity = Val(styleArr(i).Right(styleArr(i).Length - 13))
		    end if
		    i = i + 1
		  wend
		  
		  stopColor = Color.RGB(baseColor.Red, baseColor.Green, baseColor.Blue, (1 - fillOpacity) * 255)
		  
		  return stopColor
		  
		End Function
	#tag EndMethod

	#tag Method, Flags = &h0
		Sub DrawSVG(Extends g As Graphics, svg As String, x As Integer, y As Integer, w1 As Integer = -10000, h1 As Integer = -10000, sx As Integer = 0, sy As Integer = 0, w2 As Integer = -10000, h2 As Integer = -10000)
		  Var xdoc As XmlDocument
		  Var e As SVG.SVGException
		  
		  if svg.Length > 0 then
		    
		    try
		      
		      xdoc = new XmlDocument(svg)
		      renderXML g, xdoc, x, y, w1, h1, sx, sy, w2, h2
		      
		    catch xmlException As XmlException
		      
		      // invalid xml, so raise an exception
		      
		      e = new SVG.SVGException()
		      e.ErrorNumber = Integer(SVGErrorEnum.MalformedXML)
		      e.Message = "Malformed XML."
		      Raise e
		      
		    end try
		    
		  end if
		  
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h0
		Sub DrawSVG(Extends g As Graphics, svg As XmlDocument, x As Integer, y As Integer, w1 As Integer = -10000, h1 As Integer = -10000, sx As Integer = 0, sy As Integer = 0, w2 As Integer = -10000, h2 As Integer = -10000)
		  renderXML g, svg, x, y, w1, h1, sx, sy, w2, h2
		  
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub DrawTransformedPicture(Extends g As Graphics, image As Picture, matrix() As Double)
		  Var srcWidth as Integer
		  Var srcHeight as Integer 
		  Var destinationQuadrilateral() as Xojo.Point
		  Var tmpX As Integer
		  Var tmpY As Integer
		  Var startX As Integer
		  Var startY As Integer
		  Var stopX As Integer
		  Var stopY As Integer
		  Var minXY as Xojo.Point
		  Var maxXY as Xojo.Point
		  Var srcRect(3) as Xojo.Point
		  Var transMatrix(8) As Double
		  Var x As Integer
		  Var y As Integer
		  Var  factor, srcX, srcY as Double
		  Var tgtPic as Picture
		  Var srcRGB as RGBSurface
		  Var tgtRGB as RGBSurface
		  Var srcWidthM1 As Integer
		  Var srcHeightM1 As Integer
		  Var dx1, dy1, dx2, dy2 as Double 'coordinates of source points
		  Var sx1, sy1, sx2, sy2 as Integer
		  Var p1,p2,p3, p4 as Color ' temporary pixels
		  Var r, gp , b, a as Integer
		  
		  srcWidth = image.Width
		  srcHeight = image.Height
		  
		  // determine destination quadrilateral using transformation matrix
		  
		  tmpX = 0
		  tmpY = 0
		  transformPoint(tmpX, tmpY, matrix)
		  destinationQuadrilateral.Add new Xojo.Point(tmpX, tmpY)
		  
		  tmpX = srcWidth -1
		  tmpY = 0
		  transformPoint(tmpX, tmpY, matrix)
		  destinationQuadrilateral.Add new Xojo.Point(tmpX, tmpY)
		  
		  tmpX = srcWidth -1
		  tmpY = srcHeight - 1
		  transformPoint(tmpX, tmpY, matrix)
		  destinationQuadrilateral.Add new Xojo.Point(tmpX, tmpY)
		  
		  tmpX = 0
		  tmpY = srcHeight - 1
		  transformPoint(tmpX, tmpY, matrix)
		  destinationQuadrilateral.Add new Xojo.Point(tmpX, tmpY)
		  
		  ' get bounding rectangle of the quadrilateral
		  
		  getBoundingRectangle destinationQuadrilateral, minXY, maxXY
		  
		  startX = minXY.X
		  startY = minXY.Y
		  stopX = maxXY.X 
		  stopY = maxXY.Y 
		  
		  'calculate tranformation matrix
		  
		  srcRect(0) = new Xojo.Point(0,0)
		  srcRect(1) = new Xojo.Point(srcWidth -1 ,0)
		  srcRect(2) = new Xojo.Point(srcWidth - 1, srcHeight - 1)
		  srcRect(3) = new Xojo.Point(0, srcHeight - 1)
		  transMatrix = MapQuadToQuad(destinationQuadrilateral, srcRect)
		  
		  tgtPic = new Picture(g.Width, g.Height)
		  srcRGB = image.RGBSurface
		  tgtRGB = tgtPic.RGBSurface
		  
		  srcWidthM1 = srcWidth - 1
		  srcHeightM1 = srcHeight - 1
		  
		  ' for each row
		  for y = startY to stopY
		    'for each pixel
		    for x = startX to stopX
		      factor = transMatrix(2) * x + transMatrix(5) * y + transMatrix(8)
		      srcX = ( transMatrix(0) * x + transMatrix(3) * y + transMatrix(6) ) / factor
		      srcY = ( transMatrix(1) * x + transMatrix(4) * y + transMatrix(7) ) / factor
		      if srcX >= 0 and srcY >= 0 and srcX< srcWidth and srcY < srcHeight then
		        sx1 = srcX
		        if sx1 = srcWidthM1 then
		          sx2 = sx1
		        else
		          sx2 = sx1 + 1
		        end if
		        dx1 = srcX - sx1
		        dx2 = 1.0 - dx1
		        
		        sy1 = srcY
		        if sy1 = srcHeightM1 then
		          sy2 = sy1
		        else
		          sy2 = sy1 + 1
		        end if
		        dy1 = srcY - sy1
		        dy2 = 1.0 - dy1
		        
		        ' copy the pixel from the source to the target using interpolation of 4 points
		        p1 = srcRGB.Pixel(sx1, sy1)
		        p2 = srcRGB.Pixel(sx2, sy1)
		        p3 = srcRGB.Pixel(sx1, sy2)
		        p4 = srcRGB.Pixel(sx2, sy2)
		        
		        r = dy2 * ( dx2 * ( p1.red ) + dx1 * ( p2.red ) ) + dy1 * ( dx2 * ( p3.red ) + dx1 * ( p4.red ) )
		        gp = dy2 * ( dx2 * ( p1.green ) + dx1 * ( p2.green ) ) + dy1 * ( dx2 * ( p3.green ) + dx1 * ( p4.green ) )
		        b = dy2 * ( dx2 * ( p1.blue ) + dx1 * ( p2.blue ) ) + dy1 * ( dx2 * ( p3.blue ) + dx1 * ( p4.blue ) )
		        a = dy2 * ( dx2 * ( p1.Alpha ) + dx1 * ( p2.Alpha ) ) + dy1 * ( dx2 * ( p3.Alpha ) + dx1 * ( p4.Alpha ) )
		        
		        tgtRGB.Pixel(x,y) = Color.RGB(r, gp, b, a)
		      end if
		    next
		  next
		  
		  g.DrawPicture tgtPic, 0, 0
		  
		  
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub getBoundingRectangle(cloud() as Xojo.Point, byref minXY as Xojo.Point, byref maxXY as Xojo.Point)
		  Var minX as integer = 10e6
		  Var maxX as integer = -10e6
		  Var minY as integer = 10e6
		  Var maxY as integer = -10e6
		  
		  Var i as integer
		  for i = 0 to cloud.LastIndex
		    if cloud(i).x < minX then minX = cloud(i).x
		    if cloud(i).x > maxX then maxX = cloud(i).x
		    if cloud(i).y < minY then minY = cloud(i).y
		    if cloud(i).y > maxY then maxY = cloud(i).y
		  next
		  
		  minXY = new Xojo.Point(minX, minY)
		  maxXY = new Xojo.Point(maxX, maxY)
		  
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Function identityMatrix() As Double()
		  Var result() As Double = Array( _
		  1.0, 0.0, 0.0, _
		  0.0, 1.0, 0.0, _
		  0.0, 0.0, 1.0)
		  
		  return result
		  
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Function isTranslationMatrix(matrix() As Double) As Boolean
		  Var result As Boolean
		  
		  result = (matrix(0) = 1) and (matrix(1) = 0) and (matrix(3) = 0) and (matrix(4) = 1) and _
		  (matrix(6) = 0) and (matrix(7) = 0) and (matrix(8) = 1)
		  
		  return result
		  
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub loadCSS(styleData As String)
		  Var className As String
		  Var i As Integer
		  Var ch As String
		  Var dataLen As Integer
		  Var state As Integer // 0 = next class, 1 = class name, 2 = property name, 3 = property value
		  Var classProperties As JSONItem
		  Var propName As String
		  Var propValue As String
		  
		  dataLen = styleData.Length
		  state = 0 // next class
		  i = 0
		  while i < dataLen
		    ch = styleData.Middle(i, 1)
		    
		    if Asc(ch) <= 32 then
		      // do nothing
		      
		    elseif ch = "{" then
		      classProperties = new JSONItem("{}")
		      propName = ""
		      state = 2 // property name
		      
		    elseif ch = "}" then
		      if propName <> "" then
		        classProperties.Value(propName.Lowercase()) = propValue
		      end if
		      mClasses.Value(className.Trim().Lowercase()) = classProperties
		      state = 0 // next class
		      
		    elseif ch = ";" then
		      classProperties.Value(propName.Lowercase()) = propValue
		      propName = ""
		      propValue = ""
		      state = 2 // property name
		      
		    elseif ch = ":" then
		      propValue = ""
		      state = 3 // property value
		      
		    else
		      select case state
		      case 0 // next class
		        className = ch
		        state = 1 // class name
		      case 1 // class name
		        className = className + ch
		      case 2 // property name
		        propName = propName + ch
		      case 3 // property value
		        propValue = propValue + ch
		      end select
		    end if
		    
		    i = i + 1
		  wend
		  
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Function loadImage(data As String) As Picture
		  Var image As Picture
		  Var alphaImage As Picture
		  Var imageData As MemoryBlock
		  Var commaPos As Integer
		  
		  commaPos = data.IndexOf(0, ",")
		  if (commaPos >= 0) and (commaPos < data.Length - 1) then
		    try
		      imageData = DecodeBase64(data.Middle(commaPos + 1))
		      image = Picture.FromData(imageData)
		    catch e As RuntimeException
		      image = nil
		    end try
		    
		    if image <> nil then
		      alphaImage = new Picture(image.Width, image.Height)
		      alphaImage.Graphics.DrawPicture image, 0, 0
		    end if
		  end if
		  
		  return alphaImage
		  
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Function LookupDouble(Extends item As JSONItem, name As String, defaultValue As Double = 0) As Double
		  return Item.Lookup(name, defaultValue)
		  
		  
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Function LookupString(Extends item As JSONItem, name As String, defaultValue As String = "") As String
		  return item.Lookup(name, defaultValue)
		  
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Function mapQuadToQuad(Quad() as Xojo.Point) As Double()
		  Var sq(8) as Double
		  Var px, py as Double
		  
		  Var TOLERANCE as double = 1e-13
		  
		  px = quad(0).X - quad(1).X + quad(2).X - quad(3).X
		  py = quad(0).Y - quad(1).Y + quad(2).Y - quad(3).Y
		  
		  if ( ( px < TOLERANCE ) And ( px > -TOLERANCE ) And ( py < TOLERANCE ) And ( py > -TOLERANCE ) ) then
		    sq(0) = quad(1).X - quad(0).X
		    sq(3) = quad(2).X - quad(1).X
		    sq(6) = quad(0).X
		    
		    sq(1) = quad(1).Y - quad(0).Y
		    sq(4) = quad(2).Y - quad(1).Y
		    sq(7) = quad(0).Y
		    
		    sq(2) = 0.0
		    sq(5) = 0.0
		    sq(8) = 1.0
		  else
		    
		    Var dx1, dx2, dy1, dy2, del as Double
		    
		    dx1 = quad(1).X - quad(2).X
		    dx2 = quad(3).X - quad(2).X
		    dy1 = quad(1).Y - quad(2).Y
		    dy2 = quad(3).Y - quad(2).Y
		    
		    del = matrixDeterminant2x2( dx1, dx2, dy1, dy2 )
		    
		    if ( del = 0 ) then
		      return sq
		    end if
		    
		    sq(2) = matrixDeterminant2x2( px, dx2, py, dy2 ) / del
		    sq(5) = matrixDeterminant2x2( dx1, px, dy1, py ) / del
		    sq(8) = 1.0
		    
		    sq(0) = quad(1).X - quad(0).X + sq(2) * quad(1).X
		    sq(3) = quad(3).X - quad(0).X + sq(5) * quad(3).X
		    sq(6) = quad(0).X
		    
		    sq(1) = quad(1).Y - quad(0).Y + sq(2) * quad(1).Y
		    sq(4) = quad(3).Y - quad(0).Y + sq(5) * quad(3).Y
		    sq(7) = quad(0).Y
		  end if
		  
		  return sq
		  
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Function mapQuadToQuad(input() as Xojo.Point, output() as Xojo.Point) As Double()
		  Var squareToInput(8) as Double = MapQuadToQuad(input)
		  Var squareToOutput(8) as Double = MapQuadToQuad(output)
		  
		  Return matrixMultiply(matrixAdjugate(squareToInput), squareToOutput)
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Function matrix(a As Double, b As Double, c As Double, d As Double, e As Double, f As Double) As Double()
		  Var result() As Double = Array( _
		  a, c, e, _
		  b, d, f, _
		  0.0, 0.0, 1.0)
		  
		  return result
		  
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Function matrixAdjugate(a() as double) As double()
		  ' Calculates adjugate 3x3 matrix
		  
		  Var b(8) as double
		  
		  b(0) = matrixDeterminant2x2( a(4), a(7), a(5), a(8) )
		  b(1) = matrixDeterminant2x2( a(7), a(1), a(8), a(2) )
		  b(2) = matrixDeterminant2x2( a(1), a(4), a(2), a(5) )
		  b(3) = matrixDeterminant2x2( a(5), a(8), a(3), a(6) )
		  b(4) = matrixDeterminant2x2( a(8), a(2), a(6), a(0) )
		  b(5) = matrixDeterminant2x2( a(2), a(5), a(0), a(3) )
		  b(6) = matrixDeterminant2x2( a(3), a(6), a(4), a(7) )
		  b(7) = matrixDeterminant2x2( a(6), a(0), a(7), a(1) )
		  b(8) = matrixDeterminant2x2( a(0), a(3), a(1), a(4) )
		  
		  return b
		  
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Function matrixDeterminant2x2(a as double, b as double, c as double, d as double) As double
		  ' Caclculates determinant of a 2x2 matrix
		  return ( a * d - b * c )
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Function matrixMultiply(m1() As Double, m2() As Double) As Double()
		  Var result(8) As Double
		  
		  result(0) = m1(0) * m2(0) + m1(1) * m2(3) + m1(2) * m2(6)
		  result(1) = m1(0) * m2(1) + m1(1) * m2(4) + m1(2) * m2(7)
		  result(2) = m1(0) * m2(2) + m1(1) * m2(5) + m1(2) * m2(8)
		  
		  result(3) = m1(3) * m2(0) + m1(4) * m2(3) + m1(5) * m2(6)
		  result(4) = m1(3) * m2(1) + m1(4) * m2(4) + m1(5) * m2(7)
		  result(5) = m1(3) * m2(2) + m1(4) * m2(5) + m1(5) * m2(8)
		  
		  result(6) = m1(6) * m2(0) + m1(7) * m2(3) + m1(8) * m2(6)
		  result(7) = m1(6) * m2(1) + m1(7) * m2(4) + m1(8) * m2(7)
		  result(8) = m1(6) * m2(2) + m1(7) * m2(5) + m1(8) * m2(8)
		  
		  return result
		  
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub process_defs(node As XmlNode)
		  Var i As Integer
		  
		  i = 0
		  while i < node.ChildCount
		    select case node.Child(i).Name
		      
		    case "style"
		      process_style(node.Child(i))
		      
		      //case else
		      //id = node.Child(i).GetAttribute("id")
		      //if id <> "" then
		      //mNodes.Value("#" + id) = node.Child(i)
		      //end if
		      
		    end select
		    i = i + 1
		  wend
		  
		  
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub process_style(node As XmlNode)
		  Var styleData As String
		  Var typeStr As String
		  
		  typeStr = node.GetAttribute("type")
		  select case typeStr
		    
		  case "", "text/css"
		    if node.FirstChild <> nil then
		      styleData = node.FirstChild.Value
		      if styleData <> "" then
		        loadCSS(styleData)
		      end if
		    end if
		    
		  case else
		    'break
		    
		  end select
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub renderNode(node As XmlNode, g As Graphics, parentMatrix() As Double, parentStyle As JSONItem)
		  Var e As SVG.SVGException
		  Var nodeId As String
		  Var useNode As XMLNode
		  Var style As JSONItem
		  
		  select case node.Name
		    
		  case "#comment"
		    // we ignore xml comments
		    
		  case "circle"
		    render_circle(node, g, parentMatrix, parentStyle)
		    
		  case "defs"
		    process_defs(node)
		    
		  case "desc"
		    // we ignore these tags
		    
		  case "ellipse"
		    render_ellipse(node, g, parentMatrix, parentStyle)
		    
		  case "g"
		    render_g(node, g, parentMatrix, parentStyle)
		    
		  case "image"
		    render_image(node, g, parentMatrix, parentStyle)
		    
		  case "line"
		    render_line(node, g, parentMatrix, parentStyle)
		    
		  case "metadata"
		    // we ignore these tags
		    
		  case "path"
		    render_path(node, g, parentMatrix, parentStyle)
		    
		  case "polygon"
		    render_polygon(node, g, parentMatrix, parentStyle)
		    
		  case "polyline"
		    render_polyline(node, g, parentMatrix, parentStyle)
		    
		  case "rect"
		    render_rect(node, g, parentMatrix, parentStyle)
		    
		  case "style"
		    process_style(node)
		    
		  case "svg"
		    render_svg(node, g, parentMatrix, parentStyle)
		    
		  case "text"
		    render_text(node, g, parentMatrix, parentStyle)
		    
		  case "title"
		    // we ignore these tags
		    
		  case "use"
		    nodeId = node.GetAttribute("xlink:href")
		    if mNodes.HasKey(nodeId) then
		      useNode = mNodes.Value(nodeId)
		      
		      style = new JSONItem("{}")
		      style.ApplyValues parentStyle
		      style.ApplyValues buildStyleItem(node)
		      
		      renderNode(useNode, g, parentMatrix, style)
		    else
		      e = new SVG.SVGException()
		      e.ErrorNumber = Integer(SVGErrorEnum.NodeNotFound)
		      e.Message = "Node not found: " + nodeId
		      Raise e
		    end if
		    
		  case else
		    
		    if node.Name.Left(9) <> "sodipodi:" then // we ignore sodipodi tags
		      
		      // we only want to raise the unknown element exception during debugging,
		      // and during runtime we simply ignore unknown elements
		      
		      #if DebugBuild then
		        e = new SVG.SVGException()
		        e.ErrorNumber = Integer(SVGErrorEnum.UnknownElement)
		        e.Message = "Unknown element: " + node.Name
		        Raise e
		      #endif
		      
		    end if
		    
		  end select
		  
		  
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub RenderPath(g As Graphics, path As GraphicsPath, style As JSONItem, scale As Double, closed As Boolean, doFill As Boolean, doStroke As Boolean)
		  Var fill As String
		  Var fillOpacity As Double
		  Var stroke As String
		  Var strokeWidth As Double
		  Var drawColor As Color
		  Var fillBrush As XMLNode
		  Var brushId As String
		  Var linearBrush As LinearGradientBrush
		  Var tmpStr As String
		  Var i As Integer
		  Var tmpArr() As String
		  Var dblArr() As Double
		  
		  g.SaveState
		  
		  // fill
		  
		  fill = style.LookupString("fill", "#000000")
		  fillOpacity = 1
		  if fill.Left(4) = "url(" then
		    brushId = fill.Middle(4, fill.Length - 5)
		    if mNodes.HasKey(brushId) then
		      fillBrush = mNodes.Value(brushId)
		    end if
		  elseif (fill <> "none") and style.HasKey("fill-opacity") then
		    if Val(style.Value("fill-opacity")) = 0 then
		      fill = "none"
		    else
		      fillOpacity = Val(style.Value("fill-opacity"))
		    end if
		  end if
		  
		  if fillBrush <> nil then
		    select case fillBrush.Name
		    case "linearGradient"
		      Var x1, y1, x2, y2 As Integer
		      Var tmpDbl As Double
		      tmpStr = fillBrush.GetAttribute("x1")
		      if tmpStr.Right(1) = "%" then
		        tmpDbl = Val(tmpStr.Left(tmpStr.Length - 1)) / 100
		      else
		        tmpDbl = Val(tmpStr)
		      end if
		      x1 = path.Bounds.Left + path.Bounds.Width * tmpDbl
		      tmpStr = fillBrush.GetAttribute("y1")
		      if tmpStr.Right(1) = "%" then
		        tmpDbl = Val(tmpStr.Left(tmpStr.Length - 1)) / 100
		      else
		        tmpDbl = Val(tmpStr)
		      end if
		      y1 = path.Bounds.Top + path.Bounds.Height * tmpDbl
		      tmpStr = fillBrush.GetAttribute("x2")
		      if tmpStr.Right(1) = "%" then
		        tmpDbl = Val(tmpStr.Left(tmpStr.Length - 1)) / 100
		      else
		        tmpDbl = Val(tmpStr)
		      end if
		      x2 = path.Bounds.Left + path.Bounds.Width * tmpDbl
		      tmpStr = fillBrush.GetAttribute("y2")
		      if tmpStr.Right(1) = "%" then
		        tmpDbl = Val(tmpStr.Left(tmpStr.Length - 1)) / 100
		      else
		        tmpDbl = Val(tmpStr)
		      end if
		      y2 = path.Bounds.Top + path.Bounds.Height * tmpDbl
		      linearBrush = new LinearGradientBrush()
		      linearBrush.StartPoint = New Point(x1, y1)
		      linearBrush.EndPoint = New Point(x2, y2)
		      i = 0
		      while i < fillBrush.ChildCount
		        if fillBrush.Child(i).Name = "stop" then
		          tmpStr = fillBrush.Child(i).GetAttribute("offset")
		          if tmpStr.Right(1) = "%" then
		            tmpDbl = Val(tmpStr.Left(tmpStr.Length - 1)) / 100
		          else
		            tmpDbl = Val(tmpStr)
		          end if
		          tmpStr = fillBrush.Child(i).GetAttribute("style")
		          linearBrush.GradientStops.Add(New Pair(tmpDbl, DetermineStopColor(tmpStr)))
		        end if
		        i = i + 1
		      wend
		      g.Brush = linearBrush
		    end select
		    
		    g.FillPath path, true
		    g.Brush = nil
		    
		  elseif fill <> "none" and doFill then
		    drawColor = determineColor(fill)
		    drawColor = Color.RGB(drawColor.Red, drawColor.Green, drawColor.Blue, (1 - fillOpacity) * 255)
		    g.DrawingColor = drawColor
		    g.FillPath path, true
		  end if
		  
		  // stroke
		  
		  stroke = style.LookupString("stroke", "")
		  strokeWidth = style.LookupDouble("stroke-width", 1) * scale
		  
		  if (stroke <> "none") and (stroke <> "") and (strokeWidth > 0) and doStroke then
		    
		    if style.HasKey("stroke-dasharray") then
		      
		      tmpStr = style.Value("stroke-dasharray")
		      while tmpStr.IndexOf("  ") >= 0
		        tmpStr = tmpStr.ReplaceAll("  ", " ")
		      wend
		      
		      tmpArr = tmpStr.Split(" ")
		      Redim dblArr(-1)
		      i = 0
		      while i < tmpArr.Count
		        dblArr.Add Val(tmpArr(i)) * scale / strokeWidth
		        i = i + 1
		      wend
		      
		      g.LineDash = dblArr
		      
		    end if
		    
		    tmpStr = style.Lookup("stroke-linecap", "") 
		    select case tmpStr
		    case "butt"
		      g.LineCap = Graphics.LineCapTypes.Butt
		    case "round"
		      g.LineCap = Graphics.LineCapTypes.Round
		    case "square"
		      g.LineCap = Graphics.LineCapTypes.Square
		    case else
		      g.LineCap = Graphics.LineCapTypes.Butt
		    end select
		    
		    tmpStr = style.Lookup("stroke-linejoin", "") 
		    select case tmpStr
		    case "miter"
		      g.LineJoin = Graphics.LineJoinTypes.Miter
		    case "round"
		      g.LineJoin = Graphics.LineJoinTypes.Round
		    case "bevel"
		      g.LineJoin = Graphics.LineJoinTypes.Bevel
		    end select
		    
		    g.DrawingColor = determineColor(stroke)
		    g.PenSize = strokeWidth
		    g.DrawPath path, closed
		  end if
		  
		  g.RestoreState()
		  
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub renderXML(g As Graphics, xdoc As XmlDocument, x As Integer, y As Integer, w1 As Integer = -10000, h1 As Integer = -10000, sx As Integer = 0, sy As Integer = 0, w2 As Integer = -10000, h2 As Integer = -10000)
		  Var i As Integer
		  Var matrix() As Double
		  Var w As Double
		  Var h As Double
		  Var wStr As String
		  Var hStr As String
		  Var svgImage As Picture
		  //Var viewbox As String
		  //Var viewboxArr() As String
		  //Var xScale As Double
		  //Var yScale As Double
		  //Var scale As Double
		  //Var xOffset As Double
		  //Var yOffset As Double
		  //Var mulMatrix() As Double
		  
		  mClasses = new JSONItem("{}")
		  mNodes = new Dictionary()
		  matrix = identityMatrix()
		  
		  i = 0
		  while (i < xdoc.ChildCount) 
		    if xdoc.Child(i).Name = "svg" then
		      
		      BuildNodeDictionary(xdoc.Child(i))
		      
		      // determine graphics context width and height
		      
		      w = 0
		      h = 0
		      
		      wStr = xdoc.Child(i).GetAttribute("width").Trim()
		      if wStr <> "" then
		        if IsNumeric(wStr) then
		          w = Val(wStr)
		        elseif wStr.Right(1) = "%" then
		          w = g.Width * (Val(wStr.Left(wStr.Length - 1)) / 100)
		        end if
		      end if
		      
		      hStr = xdoc.Child(i).GetAttribute("height").Trim()
		      if hStr <> "" then
		        if IsNumeric(hStr) then
		          h = Val(hStr)
		        elseif hStr.Right(1) = "%" then
		          h = g.Height * (Val(hStr.Left(hStr.Length - 1)) / 100)
		        end if
		      end if
		      
		      if w = 0 then
		        w = g.Width
		      end if
		      if h = 0 then
		        h = g.Height
		      end if
		      
		      // apply viewbox if there is one
		      
		      //viewbox = Trim(xdoc.Child(i).GetAttribute("viewbox"))
		      //if viewbox <> "" then
		      //while viewbox.InStr(0, "  ") > 0 
		      //viewbox = viewbox.ReplaceAll("  ", " ")
		      //wend
		      //viewboxArr = viewbox.Split(" ")
		      //if viewboxArr.LastIndex = 3 then
		      //xScale = w / Val(viewboxArr(2)) 
		      //yScale = h / Val(viewboxArr(3)) 
		      //if xScale < yScale then
		      //scale = xScale
		      //xOffset = 0
		      //yOffset = (h - (Val(viewboxArr(3))  * scale)) / 2
		      //else
		      //scale = yScale
		      //xOffset = (w - (Val(viewboxArr(2))  * scale)) / 2
		      //yOffset = 0
		      //end if
		      //mulMatrix = translationMatrix(xOffset, yOffset)
		      //matrix = matrixMultiply(matrix, mulMatrix)
		      //mulMatrix = scaleMatrix(scale, scale)
		      //matrix = matrixMultiply(matrix, mulMatrix)
		      //
		      //end if
		      //end if
		      
		      //' Smoohing algoritm courtesy of Marco Hof.
		      //
		      //mulMatrix = initScaleMatrix(2, 2)
		      //matrix = matrixMultiply(matrix, mulMatrix)
		      
		      //svgImage = new Picture(w * 2, h * 2)
		      //renderNode(xdoc.Child(i), svgImage.Graphics, matrix, new JSONItem("{}"))
		      //finalImage = svgImage.ScalePicture(w, h)
		      //g.DrawPicture finalImage, x, y, w1, h1, sx, sy, w2, h2
		      
		      svgImage = new Picture(w, h)
		      renderNode(xdoc.Child(i), svgImage.Graphics, matrix, new JSONItem("{}"))
		      
		      g.DrawPicture svgImage, x, y, w1, h1, sx, sy, w2, h2
		      
		    end if
		    i = i + 1
		  wend
		  
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub render_circle(node As XmlNode, g As Graphics, parentMatrix() As Double, parentStyle As JSONItem)
		  Var localStyle As JSONItem
		  Var style As JSONItem
		  Var matrix() As Double
		  Var i As Integer
		  Var tmpX As Double
		  Var tmpY As Double
		  Var cx As Double
		  Var cy As Double
		  Var r As Double
		  Var pointCount As Integer
		  Var theta As Double
		  Var path As GraphicsPath
		  
		  style = new JSONItem("{}")
		  style.ApplyValues parentStyle
		  localStyle = buildStyleItem(node)
		  style.ApplyValues localStyle
		  matrix = buildTransformationMatrix(localStyle.Lookup("transform", ""))
		  matrix = matrixMultiply(parentMatrix, matrix)
		  
		  cx = style.LookupDouble("cx")
		  cy = style.LookupDouble("cy")
		  r = style.LookupDouble("r") 
		  
		  if (r > 0) then
		    
		    // build path
		    
		    path = new GraphicsPath()
		    
		    pointCount = 128
		    i = 0
		    
		    theta = Pi * (i / (pointCount / 2))
		    tmpX = cx + r * cos(theta) // center a + radius x * cos(theta)
		    tmpY = cy + r * sin(theta) // center b + radius y * sin(theta)
		    transformPoint tmpX, tmpY, matrix
		    path.MoveToPoint tmpX, tmpY
		    
		    i = 1 
		    while i <= pointCount 
		      theta = Pi * (i / (pointCount / 2))
		      tmpX = cx + r * cos(theta) // center a + radius x * cos(theta)
		      tmpY = cy + r * sin(theta) // center b + radius y * sin(theta)
		      transformPoint tmpX, tmpY, matrix
		      
		      path.AddLineToPoint tmpX, tmpY
		      
		      i = i + 1
		    wend
		    
		    RenderPath g, path, style, matrix(0), true, true, true
		    
		  end if
		  
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub render_ellipse(node As XmlNode, g As Graphics, parentMatrix() As Double, parentStyle As JSONItem)
		  Var localStyle As JSONItem
		  Var style As JSONItem
		  Var matrix() As Double
		  Var i As Integer
		  Var tmpX As Double
		  Var tmpY As Double
		  Var cx As Double
		  Var cy As Double
		  Var rx As Double
		  Var ry As Double
		  Var pointCount As Integer
		  Var theta As Double
		  Var path As GraphicsPath
		  
		  style = new JSONItem("{}")
		  style.ApplyValues parentStyle
		  localStyle = buildStyleItem(node)
		  style.ApplyValues localStyle
		  matrix = buildTransformationMatrix(localStyle.Lookup("transform", ""))
		  matrix = matrixMultiply(parentMatrix, matrix)
		  
		  cx = style.LookupDouble("cx")
		  cy = style.LookupDouble("cy")
		  rx = style.LookupDouble("rx")
		  ry = style.LookupDouble("ry")
		  
		  if (rx > 0) and (ry > 0) then
		    
		    // build path
		    
		    path = new GraphicsPath()
		    
		    pointCount = 128
		    i = 0
		    
		    theta = Pi * (i / (pointCount / 2))
		    tmpX = cx + rx * cos(theta) // center a + radius x * cos(theta)
		    tmpY = cy + ry * sin(theta) // center b + radius y * sin(theta)
		    transformPoint tmpX, tmpY, matrix
		    path.MoveToPoint tmpX, tmpY
		    
		    while i <= pointCount 
		      theta = Pi * (i / (pointCount / 2))
		      tmpX = cx + rx * cos(theta) // center a + radius x * cos(theta)
		      tmpY = cy + ry * sin(theta) // center b + radius y * sin(theta)
		      transformPoint tmpX, tmpY, matrix
		      
		      path.AddLineToPoint tmpX, tmpY
		      
		      i = i + 1
		    wend
		    
		    RenderPath g, path, style, matrix(0), true, true, true
		    
		  end if
		  
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub render_g(node As XmlNode, g As Graphics, parentMatrix() As Double, parentStyle As JSONItem)
		  Var localStyle As JSONItem
		  Var style As JSONItem
		  Var matrix() As Double
		  Var i As Integer
		  
		  style = new JSONItem("{}")
		  style.ApplyValues parentStyle
		  localStyle = buildStyleItem(node)
		  style.ApplyValues localStyle
		  matrix = buildTransformationMatrix(localStyle.Lookup("transform", ""))
		  matrix = matrixMultiply(parentMatrix, matrix)
		  
		  i = 0
		  while i < node.ChildCount
		    renderNode node.Child(i), g, matrix, style
		    i = i + 1
		  wend
		  
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub render_image(node As XmlNode, g As Graphics, parentMatrix() As Double, parentStyle As JSONItem)
		  
		  Var localStyle As JSONItem
		  Var style As JSONItem
		  Var matrix() As Double
		  Var mulMatrix() As Double
		  Var imageData As String
		  Var image As Picture
		  Var x As Double
		  Var y As Double
		  Var width As Double
		  Var height As Double
		  
		  style = new JSONItem("{}")
		  style.ApplyValues parentStyle
		  localStyle = buildStyleItem(node)
		  style.ApplyValues localStyle
		  matrix = buildTransformationMatrix(localStyle.Lookup("transform", ""))
		  matrix = matrixMultiply(parentMatrix, matrix)
		  
		  x = style.LookupDouble("x")
		  y = style.LookupDouble("y")
		  width = style.LookupDouble("width")
		  height = style.LookupDouble("height")
		  
		  imageData = node.GetAttribute("xlink:href")
		  image = loadImage(imageData)
		  
		  if image <> nil then
		    
		    mulMatrix = translationMatrix(x, y)
		    matrix = matrixMultiply(matrix, mulMatrix)
		    
		    // to speed up rendering, we only use DrawTransformedPicture when needed
		    
		    if isTranslationMatrix(matrix) then
		      g.DrawPicture image, matrix(2), matrix(5)
		    else
		      g.DrawTransformedPicture image, matrix
		    end if
		    
		  end if
		  
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub render_line(node As XmlNode, g As Graphics, parentMatrix() As Double, parentStyle As JSONItem)
		  Var localStyle As JSONItem
		  Var style As JSONItem
		  Var matrix() As Double
		  Var x1 As Double
		  Var y1 As Double
		  Var x2 As Double
		  Var y2 As Double
		  Var path As GraphicsPath
		  
		  style = new JSONItem("{}")
		  style.ApplyValues parentStyle
		  localStyle = buildStyleItem(node)
		  style.ApplyValues localStyle
		  matrix = buildTransformationMatrix(localStyle.Lookup("transform", ""))
		  matrix = matrixMultiply(parentMatrix, matrix)
		  
		  x1 = style.LookupDouble("x1") 
		  y1 = style.LookupDouble("y1") 
		  x2 = style.LookupDouble("x2") 
		  y2 = style.LookupDouble("y2") 
		  
		  // build path
		  
		  path = new GraphicsPath()
		  
		  transformPoint x1, y1, matrix
		  path.MoveToPoint x1, y1
		  
		  transformPoint x2, y2, matrix
		  path.AddLineToPoint x2, y2 
		  
		  RenderPath g, path, style, matrix(0), false, false, true
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub render_path(node As XmlNode, g As Graphics, parentMatrix() As Double, parentStyle As JSONItem)
		  Var localStyle As JSONItem
		  Var style As JSONItem
		  Var matrix() As Double
		  Var i As UInt64
		  Var fill As String
		  Var stroke As String
		  Var strokeWidth As Double
		  Var d As String
		  Var ch As String
		  Var penX As Double
		  Var penY As Double
		  Var tmpX As Double
		  Var tmpY As Double
		  Var path() As String
		  Var continueImplicit As Boolean
		  Var prevCCommand As Boolean
		  Var prevQCommand As Boolean
		  Var prevControlX As Double
		  Var prevControlY As Double
		  Var itemFill As Boolean
		  Var itemFillColor As Color
		  Var itemStroke As Boolean
		  Var itemStrokeColor As Color
		  Var prevClosed As Boolean
		  Var additionalPath() As String
		  Var e As SVG.SVGException
		  Var x1 As Double
		  Var y1 As Double
		  Var x2 As Double
		  Var y2 As Double
		  Var flagA As Integer
		  Var flagS As Integer
		  Var rx As Double
		  Var ry As Double
		  Var theta As Double
		  Var x1Comp As Double
		  Var y1Comp As Double
		  Var cxComp As Double
		  Var cyComp As Double
		  Var cx As Double
		  Var cy As Double
		  Var theta1 As Integer
		  Var thetaDelta As Integer
		  Var tmpDbl As Double
		  Var currentAngle As Double
		  Var angleStep As Double
		  Var pathMB As MemoryBlock
		  Var adjustValue As Integer
		  Var relativeCommand As Boolean
		  Var tmpMatrix() As Double
		  Var tmpMatrix2() As Double
		  Var radiScale As Double
		  Var shape As GraphicsPath
		  Var ux As Double
		  Var uy As Double
		  Var vx As Double
		  Var vy As Double
		  Var controlX1 As Double
		  Var controlY1 As Double
		  Var controlX2 As Double
		  Var controlY2 As Double
		  Var tmpStr As String
		  Var startX As Double
		  Var startY As Double
		  
		  shape = new GraphicsPath()
		  
		  style = new JSONItem("{}")
		  style.ApplyValues parentStyle
		  localStyle = buildStyleItem(node)
		  style.ApplyValues localStyle
		  matrix = buildTransformationMatrix(localStyle.Lookup("transform", ""))
		  matrix = matrixMultiply(parentMatrix, matrix)
		  
		  fill = style.LookupString("fill", "#000000")
		  if (fill <> "none") and style.HasKey("fill-opacity") then
		    if Val(style.Value("fill-opacity")) = 0 then
		      fill = "none"
		    elseif Val(style.Value("fill-opacity")) = 1 then
		      // do nothing
		    else
		      'break // todo
		    end if
		  end if
		  stroke = style.LookupString("stroke", "")
		  strokeWidth = style.LookupDouble("stroke-width", 1) * matrix(0)
		  
		  // fill
		  
		  if (fill <> "none") then
		    itemFill = true
		    itemFillColor = determineColor(fill)
		  else
		    itemFill = false
		  end if
		  
		  // stroke
		  
		  if (stroke <> "none") and (stroke <> "") and (strokeWidth > 0) then
		    itemStroke = true
		    itemStrokeColor = determineColor(stroke)
		  else
		    itemStroke = false
		  end if
		  
		  // build figure shape
		  
		  tmpStr = parentStyle.Lookup("x", "0")
		  penX = Val(tmpStr)
		  tmpStr = parentStyle.Lookup("y", "0")
		  penY = Val(tmpStr)
		  prevClosed = false
		  
		  tmpX = penX
		  tmpY = penY
		  transformPoint tmpX, tmpY, matrix
		  shape.MoveToPoint tmpX, tmpY
		  
		  d = style.LookupString("d", "").Trim()
		  d = d.ReplaceAll(",", " ")
		  
		  pathMB = d
		  
		  Redim path(-1)
		  path.Add ""
		  i = 0
		  while i < pathMB.Size
		    ch = pathMB.StringValue(i, 1)
		    
		    if ch = " " then
		      
		      if path(path.LastIndex) <> "" then
		        path.Add ""
		      end if
		      
		    elseif ch = "-" then
		      
		      if path(path.LastIndex) <> "" then
		        if path(path.LastIndex).Right(1) = "e" then
		          path(path.LastIndex) = path(path.LastIndex) + ch
		        else
		          path.Add "-"
		        end if
		      else
		        path(path.LastIndex) = ch
		      end if
		      
		    elseif not IsNumeric(ch) and (ch <> ".") and (ch <> "-") and (ch <> "e") then
		      
		      if path(path.LastIndex) <> "" then
		        path.Add ch
		      else
		        path(path.LastIndex) = ch
		      end if
		      path.Add ""
		      
		    elseif ch = "." then
		      
		      if path(path.LastIndex).IndexOf(0, ".") >= 0 then
		        path.Add "."
		      else
		        path(path.LastIndex) = path(path.LastIndex) + ch
		      end if
		      
		    else
		      
		      path(path.LastIndex) = path(path.LastIndex) + ch
		      
		    end if
		    i = i + 1
		  wend
		  
		  if path(path.LastIndex) = "" then
		    path.RemoveAt(path.LastIndex)
		  end if
		  
		  if additionalPath.LastIndex > 4 then
		    additionalPath.Add "z"
		    if relativeCommand then
		      additionalPath.Add "M"
		      additionalPath.Add "0"
		      additionalPath.Add "0"
		    end if
		    
		    i = 0
		    while i <= additionalPath.LastIndex
		      path.AddAt(i, additionalPath(i))
		      i = i + 1
		    wend
		  end if
		  
		  // draw path
		  
		  prevCCommand = false
		  prevQCommand = false
		  
		  i = 0
		  while i <= path.LastIndex
		    
		    // absolute elliptical arc AND relative elliptical arc
		    
		    if (path(i).Compare("A", ComparisonOptions.CaseSensitive) = 0) or _
		      (path(i).Compare("a", ComparisonOptions.CaseSensitive) = 0) then 
		      
		      Var isAbsolute As Boolean
		      if path(i).Compare("A", ComparisonOptions.CaseSensitive) = 0 then
		        isAbsolute = true
		      else
		        isAbsolute = false
		      end if
		      
		      do
		        
		        x1 = penX
		        y1 = penY
		        i = i + 1
		        rx = Val(path(i))
		        i = i + 1
		        ry = Val(path(i))
		        i = i + 1
		        theta = Val(path(i)) * DegToRad
		        i = i + 1
		        flagA = Val(path(i))
		        i = i + 1
		        flagS = Val(path(i))
		        
		        if isAbsolute then
		          i = i + 1
		          x2 = Val(path(i))
		          i = i + 1
		          y2 = Val(path(i))
		        else
		          i = i + 1
		          x2 = penX + Val(path(i))
		          i = i + 1
		          y2 = penY + Val(path(i))
		        end if
		        
		        // Step 1: Compute (x1', y1')
		        
		        x1Comp = cos(theta) * ((x1 - x2) / 2) +  sin(theta) * ((y1 - y2) / 2)
		        y1Comp = -sin(theta) * ((x1 - x2) / 2) +  cos(theta) * ((y1 - y2) / 2)
		        
		        // correction of out-of-range radii
		        
		        radiScale = (x1Comp^2 / rx^2) + (y1Comp^2 / ry^2)
		        if radiScale > 1 then
		          rx = Sqrt(radiScale) * rx
		          ry = Sqrt(radiScale) * ry
		        end if
		        
		        // Step 2: Compute(cx', cy')
		        
		        tmpDbl = (rx^2 * ry^2) - (rx^2 * y1Comp^2) - (ry^2 * x1Comp^2)
		        tmpDbl = tmpDbl / ((rx^2 * y1Comp^2) + (ry^2 * x1Comp^2))
		        if tmpDbl < 0 then
		          tmpDbl = 0
		        end if
		        tmpDbl = Sqrt(tmpDbl)
		        
		        if flagA = flagS then
		          tmpDbl = -tmpDbl
		        end if
		        
		        cxComp = tmpDbl * (rx * y1Comp / ry)
		        cyComp = tmpDbl * -(ry * x1Comp / rx)
		        
		        // Step 3: Compute (cx, cy) from (cx', cy')
		        
		        cx = (cos(theta) * cxComp - sin(theta) * cyComp) + ((x1 + x2) / 2)
		        cy = (sin(theta) * cxComp + cos(theta) * cyComp) + ((y1 + y2) / 2)
		        
		        // Step 4: Compute theta1 and thetaDelta
		        
		        ux = 1
		        uy = 0
		        vx = (x1Comp - cxComp) / rx
		        vy = (y1Comp - cyComp) / ry
		        theta1 = angleBetweenVectors(ux, uy, vx, vy)
		        
		        ux = (x1Comp - cxComp) / rx
		        uy = (y1Comp - cyComp) / ry
		        vx = (-x1Comp - cxComp) / rx
		        vy = (-y1Comp - cyComp) / ry
		        thetaDelta = angleBetweenVectors(ux, uy, vx, vy)
		        thetaDelta = thetaDelta mod 360
		        
		        if (flagS = 0) and (thetaDelta > 0) then
		          thetaDelta = thetaDelta - 360
		        elseif (flagS = 1) and (thetaDelta < 0) then
		          thetaDelta = thetaDelta + 360
		        end if
		        
		        // Build path using calculated values
		        
		        if thetaDelta <> 0 then
		          
		          adjustValue = thetaDelta / Abs(thetaDelta)
		          
		          angleStep = (thetaDelta / 360) 
		          
		          currentAngle = theta1 + angleStep
		          
		          tmpMatrix = translationMatrix(0, 0) 
		          
		          tmpMatrix2 = translationMatrix(cx, cy)
		          tmpMatrix = matrixMultiply(tmpMatrix, tmpMatrix2)
		          tmpMatrix2 = rotationMatrix(theta * RadToDeg)
		          tmpMatrix = matrixMultiply(tmpMatrix, tmpMatrix2)
		          tmpMatrix2 = translationMatrix(-cx, -cy)
		          tmpMatrix = matrixMultiply(tmpMatrix, tmpMatrix2)
		          
		          tmpMatrix = matrixMultiply(tmpMatrix, matrix)
		          
		          // build arc path
		          
		          while currentAngle * adjustValue <= (theta1 + thetaDelta) * adjustValue
		            
		            tmpX = cx + rx  * cos(currentAngle * DegToRad) 
		            tmpY = cy + ry * sin(currentAngle * DegToRad) 
		            
		            transformPoint tmpX, tmpY, tmpMatrix
		            
		            shape.AddLineToPoint tmpX, tmpY 
		            
		            currentAngle = currentAngle + angleStep
		            
		          wend 
		          
		        end if
		        
		        penX = x2
		        penY = y2
		        
		        continueImplicit = false
		        if i < path.LastIndex then
		          if IsNumeric(path(i + 1)) then
		            continueImplicit = true
		          end if
		        end if
		        
		      loop until not continueImplicit
		      
		      prevCCommand = false
		      prevQCommand = false
		      
		    elseif path(i).Compare("C", ComparisonOptions.CaseSensitive) = 0 then // absolute curveto
		      do
		        i = i + 1
		        tmpX = Val(path(i))
		        i = i + 1
		        tmpY = Val(path(i))
		        transformPoint tmpX, tmpY, matrix
		        controlX1 = tmpX
		        controlY1 = tmpY
		        i = i + 1
		        tmpX = Val(path(i))
		        i = i + 1
		        tmpY = Val(path(i))
		        transformPoint tmpX, tmpY, matrix
		        controlX2 = tmpX
		        controlY2 = tmpY
		        prevControlX = tmpX
		        prevControlY = tmpY
		        i = i + 1
		        tmpX = Val(path(i))
		        i = i + 1
		        tmpY = Val(path(i))
		        penX = tmpX
		        penY = tmpY
		        transformPoint tmpX, tmpY, matrix
		        
		        shape.AddCurveToPoint controlX1, controlY1, controlX2, controlY2, tmpX, tmpY
		        
		        continueImplicit = false
		        if i < path.LastIndex then
		          if IsNumeric(path(i + 1)) then
		            continueImplicit = true
		          end if
		        end if
		        
		      loop until not continueImplicit
		      
		      prevCCommand = true
		      prevQCommand = false
		      
		    elseif path(i).Compare("c", ComparisonOptions.CaseSensitive) = 0 then // relative curveto
		      do
		        i = i + 1
		        tmpX = penX + Val(path(i))
		        i = i + 1
		        tmpY = penY + Val(path(i))
		        transformPoint tmpX, tmpY, matrix
		        controlX1 = tmpX
		        controlY1 = tmpY
		        i = i + 1
		        tmpX = penX + Val(path(i))
		        i = i + 1
		        tmpY = penY + Val(path(i))
		        transformPoint tmpX, tmpY, matrix
		        controlX2 = tmpX
		        controlY2 = tmpY
		        prevControlX = tmpX 
		        prevControlY = tmpY
		        i = i + 1
		        tmpX = penX + Val(path(i))
		        i = i + 1
		        tmpY = penY + Val(path(i))
		        penX = tmpX 
		        penY = tmpY
		        transformPoint tmpX, tmpY, matrix
		        
		        shape.AddCurveToPoint controlX1, controlY1, controlX2, controlY2, tmpX, tmpY
		        
		        continueImplicit = false
		        if i < path.LastIndex then
		          if IsNumeric(path(i + 1)) then
		            continueImplicit = true
		          end if
		        end if
		        
		      loop until not continueImplicit
		      
		      prevCCommand = true
		      prevQCommand = false
		      
		    elseif path(i).Compare("H", ComparisonOptions.CaseSensitive) = 0 then // absolute horizontal lineto
		      do
		        i = i + 1
		        tmpX = Val(path(i))
		        penX = tmpX
		        tmpY = penY
		        transformPoint tmpX, tmpY, matrix
		        
		        shape.AddLineToPoint tmpX, tmpY
		        
		        continueImplicit = false
		        if i < path.LastIndex then
		          if IsNumeric(path(i + 1)) then
		            continueImplicit = true
		          end if
		        end if
		        
		      loop until not continueImplicit
		      
		      prevCCommand = false
		      prevQCommand = false
		      
		    elseif path(i).Compare("h", ComparisonOptions.CaseSensitive) = 0 then // relative horizontal lineto
		      do
		        i = i + 1
		        tmpX = penX + Val(path(i))
		        penX = tmpX
		        tmpY = penY
		        transformPoint tmpX, tmpY, matrix
		        
		        shape.AddLineToPoint tmpX, tmpY
		        
		        continueImplicit = false
		        if i < path.LastIndex then
		          if IsNumeric(path(i + 1)) then
		            continueImplicit = true
		          end if
		        end if
		        
		      loop until not continueImplicit
		      
		      prevCCommand = false
		      prevQCommand = false
		      
		    elseif path(i).Compare("L", ComparisonOptions.CaseSensitive) = 0 then // absolute lineto
		      
		      do
		        
		        i = i + 1
		        tmpX = Val(path(i))
		        i = i + 1
		        tmpY = Val(path(i))
		        penX = tmpX
		        penY = tmpY
		        transformPoint tmpX, tmpY, matrix
		        
		        shape.AddLineToPoint tmpX, tmpY
		        
		        continueImplicit = false
		        if i < path.LastIndex then
		          if IsNumeric(path(i + 1)) then
		            continueImplicit = true
		          end if
		        end if
		        
		      loop until not continueImplicit
		      
		      prevCCommand = false
		      prevQCommand = false
		      
		    elseif path(i).Compare("l", ComparisonOptions.CaseSensitive) = 0 then // relative lineto
		      
		      do
		        i = i + 1
		        tmpX = penX + Val(path(i))
		        i = i + 1
		        tmpY = penY + Val(path(i))
		        penX = tmpX
		        penY = tmpY
		        transformPoint tmpX, tmpY, matrix
		        
		        shape.AddLineToPoint tmpX, tmpY
		        
		        continueImplicit = false
		        if i < path.LastIndex then
		          if IsNumeric(path(i + 1)) then
		            continueImplicit = true
		          end if
		        end if
		        
		      loop until not continueImplicit
		      
		      prevCCommand = false
		      prevQCommand = false
		      
		    elseif path(i).Compare("M", ComparisonOptions.CaseSensitive) = 0 then // absolute move
		      
		      i = i + 1
		      tmpX = Val(path(i))
		      i = i + 1
		      tmpY = Val(path(i))
		      
		      penX = tmpX
		      penY = tmpY
		      startX = penX
		      startY = penY
		      
		      transformPoint tmpX, tmpY, matrix
		      shape.MoveToPoint tmpX, tmpY
		      
		      // apply  implicit lineto commands
		      
		      do
		        continueImplicit = false
		        if i < (path.LastIndex - 1) then
		          if IsNumeric(path(i + 1)) then
		            i = i + 1
		            tmpX = Val(path(i))
		            i = i + 1
		            tmpY = Val(path(i))
		            penX = tmpX
		            penY = tmpY
		            transformPoint tmpX, tmpY, matrix
		            
		            shape.AddLineToPoint tmpX, tmpY
		            
		            continueImplicit = true
		          end if
		        end if
		      loop until (i > path.LastIndex) or not continueImplicit
		      
		      prevCCommand = false
		      prevQCommand = false
		      
		    elseif path(i).Compare("m", ComparisonOptions.CaseSensitive) = 0 then // relative move
		      
		      i = i + 1
		      tmpX = Val(path(i))
		      i = i + 1
		      tmpY = Val(path(i))
		      
		      penX = penX + tmpX
		      penY = penY + tmpY
		      startX = penX
		      startY = penY
		      
		      tmpX = penX
		      tmpY = penY
		      
		      transformPoint tmpX, tmpY, matrix
		      shape.MoveToPoint tmpX, tmpY
		      
		      // apply  implicit lineto commands
		      
		      do
		        continueImplicit = false
		        if i < (path.LastIndex - 1) then
		          if IsNumeric(path(i + 1)) then
		            i = i + 1
		            tmpX = Val(path(i))
		            i = i + 1
		            tmpY = Val(path(i))
		            penX = penX + tmpX
		            penY = penY + tmpY
		            tmpX = penX
		            tmpY = penY
		            transformPoint tmpX, tmpY, matrix
		            
		            shape.AddLineToPoint tmpX, tmpY
		            
		            continueImplicit = true
		          end if
		        end if
		      loop until (i > path.LastIndex) or not continueImplicit
		      
		      prevCCommand = false
		      prevQCommand = false
		      
		    elseif path(i).Compare("Q", ComparisonOptions.CaseSensitive) = 0 then // absolute quadratic Bézier curveto
		      do
		        i = i + 1
		        tmpX = Val(path(i))
		        i = i + 1
		        tmpY = Val(path(i))
		        transformPoint tmpX, tmpY, matrix
		        controlX1 = tmpX
		        controlY1 = tmpY
		        prevControlX = tmpX
		        prevControlY = tmpY
		        i = i + 1
		        tmpX = Val(path(i))
		        i = i + 1
		        tmpY = Val(path(i))
		        penX = tmpX
		        penY = tmpY
		        transformPoint tmpX, tmpY, matrix
		        
		        shape.AddQuadraticCurveToPoint controlX1, controlY1, tmpX, tmpY
		        
		        continueImplicit = false
		        if i < path.LastIndex then
		          if IsNumeric(path(i + 1)) then
		            continueImplicit = true
		          end if
		        end if
		        
		      loop until not continueImplicit
		      
		      prevCCommand = false
		      prevQCommand = true
		      
		    elseif path(i).Compare("q", ComparisonOptions.CaseSensitive) = 0 then // relative quadratic Bézier curveto
		      do
		        
		        tmpX = penX
		        tmpY = penY
		        transformPoint tmpX, tmpY, matrix
		        i = i + 1
		        tmpX = penX + Val(path(i))
		        i = i + 1
		        tmpY = penY + Val(path(i))
		        transformPoint tmpX, tmpY, matrix
		        prevControlX = tmpX
		        prevControlY = tmpY
		        i = i + 1
		        tmpX = penX + Val(path(i))
		        i = i + 1
		        tmpY = penY + Val(path(i))
		        penX = tmpX
		        penY = tmpY
		        transformPoint tmpX, tmpY, matrix
		        //cs.X2 = tmpX
		        //cs.Y2 = tmpY
		        
		        // TODO: draw shape
		        shape.AddQuadraticCurveToPoint prevControlX, prevControlY, tmpX, tmpY
		        
		        continueImplicit = false
		        if i < path.LastIndex then
		          if IsNumeric(path(i + 1)) then
		            continueImplicit = true
		          end if
		        end if
		        
		      loop until not continueImplicit
		      
		      prevCCommand = false
		      prevQCommand = true
		      
		    elseif path(i).Compare("S", ComparisonOptions.CaseSensitive) = 0 then // absolute smooth curveto
		      
		      do
		        
		        tmpX = penX
		        tmpY = penY
		        transformPoint tmpX, tmpY, matrix
		        
		        if prevCCommand then
		          controlX1 = (tmpX - prevControlX) + tmpX
		          controlY1 = (tmpY - prevControlY) + tmpY
		        else
		          controlX1 = tmpX
		          controlY1 = tmpY
		        end if
		        i = i + 1
		        tmpX = Val(path(i))
		        i = i + 1
		        tmpY = Val(path(i))
		        transformPoint tmpX, tmpY, matrix
		        controlX2 = tmpX
		        controlY2 = tmpY
		        prevControlX = tmpX
		        prevControlY = tmpY
		        i = i + 1
		        tmpX = Val(path(i))
		        i = i + 1
		        tmpY = Val(path(i))
		        penX = tmpX
		        penY = tmpY
		        transformPoint tmpX, tmpY, matrix
		        
		        shape.AddCurveToPoint controlX1, controlY1, controlX2, controlY2, tmpX, tmpY
		        
		        continueImplicit = false
		        if i < path.LastIndex then
		          if IsNumeric(path(i + 1)) then
		            continueImplicit = true
		          end if
		        end if
		        
		      loop until not continueImplicit
		      
		      prevCCommand = true
		      prevQCommand = false
		      
		    elseif path(i).Compare("s", ComparisonOptions.CaseSensitive) = 0 then // relative smooth curveto
		      
		      do
		        
		        tmpX = penX
		        tmpY = penY
		        transformPoint tmpX, tmpY, matrix
		        
		        if prevCCommand then
		          controlX1 = (tmpX - prevControlX) + tmpX
		          controlY1 = (tmpY - prevControlY) + tmpY
		        else
		          controlX1 = tmpX
		          controlY1 = tmpY
		        end if
		        i = i + 1
		        tmpX = penX + Val(path(i))
		        i = i + 1
		        tmpY = penY + Val(path(i))
		        transformPoint tmpX, tmpY, matrix
		        controlX2 = tmpX
		        controlY2 = tmpY
		        prevControlX = tmpX
		        prevControlY = tmpY
		        i = i + 1
		        tmpX = penX + Val(path(i))
		        i = i + 1
		        tmpY = penY + Val(path(i))
		        penX = tmpX
		        penY = tmpY
		        transformPoint tmpX, tmpY, matrix
		        
		        shape.AddCurveToPoint controlX1, controlY1, controlX2, controlY2, tmpX, tmpY
		        
		        continueImplicit = false
		        if i < path.LastIndex then
		          if IsNumeric(path(i + 1)) then
		            continueImplicit = true
		          end if
		        end if
		        
		      loop until not continueImplicit
		      
		      prevCCommand = true
		      prevQCommand = false
		      
		    elseif path(i).Compare("T", ComparisonOptions.CaseSensitive) = 0 then // absolute smooth quadratic Bézier curveto
		      do
		        tmpX = penX
		        tmpY = penY 
		        transformPoint tmpX, tmpY, matrix
		        
		        if prevQCommand then
		          controlX1 = (tmpX - prevControlX) + tmpX
		          controlY1 = (tmpY - prevControlY) + tmpY
		        else
		          controlX1 = tmpX
		          controlY1 = tmpY
		        end if
		        prevControlX = tmpX
		        prevControlY = tmpY
		        i = i + 1
		        tmpX = Val(path(i))
		        i = i + 1
		        tmpY = Val(path(i))
		        penX = tmpX
		        penY = tmpY
		        transformPoint tmpX, tmpY, matrix
		        
		        shape.AddQuadraticCurveToPoint controlX1, controlY1, tmpX, tmpY
		        
		        continueImplicit = false
		        if i < path.LastIndex then
		          if IsNumeric(path(i + 1)) then
		            continueImplicit = true
		          end if
		        end if
		        
		      loop until not continueImplicit
		      
		      prevCCommand = false
		      prevQCommand = true
		      
		    elseif path(i).Compare("t", ComparisonOptions.CaseSensitive) = 0 then // relative smooth quadratic Bézier curveto
		      do
		        tmpX = penX
		        tmpY = penY
		        transformPoint tmpX, tmpY, matrix
		        if prevQCommand then
		          controlX1 = (tmpX - prevControlX) + tmpX
		          controlY1 = (tmpY - prevControlY) + tmpY
		        else
		          controlX1 = tmpX
		          controlY1 = tmpY
		        end if
		        prevControlX = tmpX
		        prevControlY = tmpY
		        i = i + 1
		        tmpX = penX + Val(path(i))
		        i = i + 1
		        tmpY = penY + Val(path(i))
		        penX = tmpX
		        penY = tmpY
		        transformPoint tmpX, tmpY, matrix
		        
		        shape.AddQuadraticCurveToPoint controlX1, controlY1, tmpX, tmpY
		        
		        continueImplicit = false
		        if i < path.LastIndex then
		          if IsNumeric(path(i + 1)) then
		            continueImplicit = true
		          end if
		        end if
		        
		      loop until not continueImplicit
		      
		      prevCCommand = false
		      prevQCommand = true
		      
		    elseif path(i).Compare("V", ComparisonOptions.CaseSensitive) = 0 then // absolute vertical lineto
		      
		      do
		        tmpX = penX
		        i = i + 1
		        tmpY = Val(path(i))
		        penY = tmpY
		        transformPoint tmpX, tmpY, matrix
		        
		        shape.AddLineToPoint tmpX, tmpY
		        
		        continueImplicit = false
		        if i < path.LastIndex then
		          if IsNumeric(path(i + 1)) then
		            continueImplicit = true
		          end if
		        end if
		        
		      loop until not continueImplicit
		      
		      prevCCommand = false
		      prevQCommand = false
		      
		    elseif path(i).Compare("v", ComparisonOptions.CaseSensitive) = 0 then // relative vertical lineto
		      
		      do
		        
		        tmpX = penX
		        i = i + 1
		        tmpY = penY + Val(path(i))
		        penY = tmpY
		        transformPoint tmpX, tmpY, matrix
		        
		        shape.AddLineToPoint tmpX, tmpY
		        
		        continueImplicit = false
		        if i < path.LastIndex then
		          if IsNumeric(path(i + 1)) then
		            continueImplicit = true
		          end if
		        end if
		        
		      loop until not continueImplicit
		      
		      prevCCommand = false
		      prevQCommand = false
		      
		    elseif path(i) = "z" then // close path
		      
		      prevClosed = true
		      
		      penX = startX
		      penY = startY
		      
		      prevCCommand = false
		      prevQCommand = false
		      
		    else
		      if IsNumeric(path(i)) then
		        e = new SVG.SVGException()
		        e.ErrorNumber = Integer(SVGErrorEnum.ExpectedPathCommand)
		        e.Message = "Expected path command: " + Str(path(i))
		        Raise e
		        i = path.LastIndex
		      end if
		      
		      prevCCommand = false
		      
		    end if
		    
		    if path(i) <> "z" then
		      prevClosed = false
		    end if
		    
		    i = i + 1
		  wend
		  
		  RenderPath g, shape, style, matrix(0), prevClosed, true, true
		  
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub render_polygon(node As XmlNode, g As Graphics, parentMatrix() As Double, parentStyle As JSONItem)
		  Var localStyle As JSONItem
		  Var style As JSONItem
		  Var matrix() As Double
		  Var i As Integer
		  Var tmpX As Double
		  Var tmpY As Double
		  Var path As GraphicsPath
		  Var rawPoints As String
		  Var tmpArr() As String
		  Var coord() As String
		  
		  style = New JSONItem("{}")
		  style.ApplyValues parentStyle
		  localStyle = buildStyleItem(node)
		  style.ApplyValues localStyle
		  
		  matrix = buildTransformationMatrix(localStyle.Lookup("transform", ""))
		  matrix = matrixMultiply(parentMatrix, matrix)
		  
		  // Build path
		  path = New GraphicsPath()
		  
		  rawPoints = style.LookupString("points", "")
		  tmpArr = rawPoints.Split(" ")
		  
		  // Find first valid coordinate pair
		  Var firstSetFound As Boolean = False
		  For i = 0 To tmpArr.LastIndex
		    If tmpArr(i).Trim <> "" Then
		      coord = tmpArr(i).Split(",")
		      If coord.LastIndex = 1 Then
		        tmpX = Val(coord(0))
		        tmpY = Val(coord(1))
		        transformPoint tmpX, tmpY, matrix
		        path.MoveToPoint tmpX, tmpY
		        firstSetFound = True
		        Exit For
		      End If
		    End If
		  Next
		  
		  If Not firstSetFound Then Return
		  
		  // Important: start from the NEXT point to avoid a zero-length segment
		  For i = i + 1 To tmpArr.LastIndex
		    If tmpArr(i).Trim = "" Then Continue
		    coord = tmpArr(i).Split(",")
		    If coord.LastIndex = 1 Then
		      tmpX = Val(coord(0))
		      tmpY = Val(coord(1))
		      transformPoint tmpX, tmpY, matrix
		      path.AddLineToPoint tmpX, tmpY
		    End If
		  Next
		  
		  // Render (closed = True so you get a join at the end)
		  RenderPath g, path, style, matrix(0), True, True, True
		  
		  
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub render_polyline(node As XmlNode, g As Graphics, parentMatrix() As Double, parentStyle As JSONItem)
		  Var localStyle As JSONItem
		  Var style As JSONItem
		  Var matrix() As Double
		  Var i As Integer
		  Var tmpX As Double
		  Var tmpY As Double
		  Var tmpArr() As String
		  Var coord() As String
		  Var path As GraphicsPath
		  
		  style = new JSONItem("{}")
		  style.ApplyValues parentStyle
		  localStyle = buildStyleItem(node)
		  style.ApplyValues localStyle
		  matrix = buildTransformationMatrix(localStyle.Lookup("transform", ""))
		  matrix = matrixMultiply(parentMatrix, matrix)
		  
		  // build path
		  
		  path = new GraphicsPath()
		  
		  tmpArr = style.LookupString("points", "").Split(" ")
		  
		  if tmpArr.Count > 1 then
		    
		    i = 0
		    coord = tmpArr(i).Split(",")
		    if coord.LastIndex = 1 then
		      tmpX = Val(coord(0))
		      tmpY = Val(coord(1))
		      transformPoint tmpX, tmpY, matrix
		    end if
		    path.MoveToPoint tmpX, tmpY
		    
		    i = i + 1
		    while i <= tmpArr.LastIndex
		      coord = tmpArr(i).Split(",")
		      if coord.LastIndex = 1 then
		        tmpX = Val(coord(0))
		        tmpY = Val(coord(1))
		        transformPoint tmpX, tmpY, matrix
		        path.AddLineToPoint tmpX, tmpY
		      end if
		      i = i + 1
		    wend
		    
		    RenderPath g, path, style, matrix(0), false, true, true
		    
		  end if
		  
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub render_rect(node As XmlNode, g As Graphics, parentMatrix() As Double, parentStyle As JSONItem)
		  Var localStyle As JSONItem
		  Var style As JSONItem
		  Var matrix() As Double
		  Var tmpX As Double
		  Var tmpY As Double
		  Var x As Double
		  Var y As Double
		  Var width As Double
		  Var height As Double
		  Var path As GraphicsPath
		  
		  style = new JSONItem("{}")
		  style.ApplyValues parentStyle
		  localStyle = buildStyleItem(node)
		  style.ApplyValues localStyle
		  matrix = buildTransformationMatrix(localStyle.Lookup("transform", ""))
		  matrix = matrixMultiply(parentMatrix, matrix)
		  
		  x = style.LookupDouble("x")
		  y = style.LookupDouble("y")
		  width = style.LookupDouble("width")
		  height = style.LookupDouble("height")
		  
		  if (width > 0) and (height > 0) then
		    
		    // Read rx/ry per SVG rules
		    Var rx As Double = style.LookupDouble("rx", 0.0)
		    Var ry As Double = style.LookupDouble("ry", 0.0)
		    
		    // Mirror if only one is provided
		    If rx > 0.0 And ry = 0.0 Then ry = rx
		    If ry > 0.0 And rx = 0.0 Then rx = ry
		    
		    // Clamp radii
		    rx = Min(rx, width/2.0)
		    ry = Min(ry, height/2.0)
		    
		    // build path
		    
		    path = new GraphicsPath()
		    
		    If rx <= 0.0 And ry <= 0.0 Then
		      // Sharp corners (your original path)
		      tmpX = x
		      tmpY = y
		      transformPoint tmpX, tmpY, matrix
		      path.MoveToPoint tmpX, tmpY
		      
		      tmpX = x
		      tmpY = y + height
		      transformPoint tmpX, tmpY, matrix
		      path.AddLineToPoint tmpX, tmpY
		      
		      tmpX = x + width
		      tmpY = y + height
		      transformPoint tmpX, tmpY, matrix
		      path.AddLineToPoint tmpX, tmpY
		      
		      tmpX = x + width
		      tmpY = y
		      transformPoint tmpX, tmpY, matrix
		      path.AddLineToPoint tmpX, tmpY
		      
		    Else
		      // Rounded corners via cubic Beziers (quarter-ellipses)
		      Const k As Double = 0.5522847498307936
		      
		      // Corner key points
		      Var x0 As Double = x
		      Var y0 As Double = y
		      Var x1 As Double = x + width
		      Var y1 As Double = y + height
		      
		      // Start at (x+rx, y)
		      Var P0x As Double = x0 + rx
		      Var P0y As Double = y0
		      transformPoint P0x, P0y, matrix
		      path.MoveToPoint P0x, P0y
		      
		      // Top edge: to (x+width-rx, y)
		      Var P1x As Double = x1 - rx
		      Var P1y As Double = y0
		      transformPoint P1x, P1y, matrix
		      path.AddLineToPoint P1x, P1y
		      
		      // Top-right corner: (x+width-rx, y) -> (x+width, y+ry)
		      Var A1x As Double = (x1 - rx) + k*rx
		      Var A1y As Double = y0
		      Var B1x As Double = x1
		      Var B1y As Double = (y0 + ry) - k*ry
		      Var C1x As Double = x1
		      Var C1y As Double = y0 + ry
		      transformPoint A1x, A1y, matrix
		      transformPoint B1x, B1y, matrix
		      transformPoint C1x, C1y, matrix
		      path.AddCurveToPoint A1x, A1y, B1x, B1y, C1x, C1y
		      
		      // Right edge: to (x+width, y+height-ry)
		      Var P2x As Double = x1
		      Var P2y As Double = y1 - ry
		      transformPoint P2x, P2y, matrix
		      path.AddLineToPoint P2x, P2y
		      
		      // Bottom-right corner: (x+width, y+height-ry) -> (x+width-rx, y+height)
		      Var A2x As Double = x1
		      Var A2y As Double = (y1 - ry) + k*ry
		      Var B2x As Double = (x1 - rx) + k*rx
		      Var B2y As Double = y1
		      Var C2x As Double = x1 - rx
		      Var C2y As Double = y1
		      transformPoint A2x, A2y, matrix
		      transformPoint B2x, B2y, matrix
		      transformPoint C2x, C2y, matrix
		      path.AddCurveToPoint A2x, A2y, B2x, B2y, C2x, C2y
		      
		      // Bottom edge: to (x+rx, y+height)
		      Var P3x As Double = x0 + rx
		      Var P3y As Double = y1
		      transformPoint P3x, P3y, matrix
		      path.AddLineToPoint P3x, P3y
		      
		      // Bottom-left corner: (x+rx, y+height) -> (x, y+height-ry)
		      Var A3x As Double = (x0 + rx) - k*rx
		      Var A3y As Double = y1
		      Var B3x As Double = x0
		      Var B3y As Double = (y1 - ry) + k*ry
		      Var C3x As Double = x0
		      Var C3y As Double = y1 - ry
		      transformPoint A3x, A3y, matrix
		      transformPoint B3x, B3y, matrix
		      transformPoint C3x, C3y, matrix
		      path.AddCurveToPoint A3x, A3y, B3x, B3y, C3x, C3y
		      
		      // Left edge: to (x, y+ry)
		      Var P4x As Double = x0
		      Var P4y As Double = y0 + ry
		      transformPoint P4x, P4y, matrix
		      path.AddLineToPoint P4x, P4y
		      
		      // Top-left corner: (x, y+ry) -> (x+rx, y)
		      Var A4x As Double = x0
		      Var A4y As Double = (y0 + ry) - k*ry
		      Var B4x As Double = (x0 + rx) - k*rx
		      Var B4y As Double = y0
		      Var C4x As Double = x0 + rx
		      Var C4y As Double = y0
		      transformPoint A4x, A4y, matrix
		      transformPoint B4x, B4y, matrix
		      transformPoint C4x, C4y, matrix
		      path.AddCurveToPoint A4x, A4y, B4x, B4y, C4x, C4y
		    End If
		    
		    RenderPath g, path, style, matrix(0), true, true, true
		    
		  end if
		  
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub render_svg(node As XmlNode, g As Graphics, parentMatrix() As Double, parentStyle As JSONItem)
		  Var localStyle As JSONItem
		  Var style As JSONItem
		  Var matrix() As Double
		  Var mulMatrix() As Double
		  Var i As Integer
		  Var x As Double
		  Var y As Double
		  Var width As Double
		  Var height As Double
		  Var viewBox As String
		  Var viewBoxArr() As String
		  Var minX As Double
		  Var minY As Double
		  Var viewBoxWidth As Double
		  Var viewBoxHeight As Double
		  Var preserveAspectRatio As String
		  Var align As String
		  Var meetOrSlice As String
		  Var scale As Double
		  Var scaleX As Double
		  Var scaleY As Double
		  Var xOffset As Double
		  Var yOffset As Double
		  
		  style = new JSONItem("{}")
		  style.ApplyValues parentStyle
		  localStyle = buildStyleItem(node)
		  style.ApplyValues localStyle
		  matrix = buildTransformationMatrix(localStyle.Lookup("transform", ""))
		  matrix = matrixMultiply(parentMatrix, matrix)
		  
		  x = style.LookupDouble("x")
		  y = style.LookupDouble("y")
		  width = style.LookupDouble("width")
		  height = style.LookupDouble("height")
		  
		  if width <= 0 then
		    width = g.Width
		  end if
		  if height <= 0 then
		    height = g.Height
		  end if
		  
		  mulMatrix = translationMatrix(x, y)
		  matrix = matrixMultiply(matrix, mulMatrix)
		  
		  viewBox = node.GetAttribute("viewBox").Trim()
		  if viewBox = "" then
		    viewBox = node.GetAttribute("viewbox").Trim()
		  end if
		  
		  if viewBox <> "" then
		    viewBox = viewBox.ReplaceAll(",", " ")
		    viewBox = collapseSpaces(viewBox)
		    
		    viewBoxArr = viewBox.Split(" ")
		    if viewBoxArr.LastIndex >= 3 then
		      minX = Val(viewBoxArr(0))
		      minY = Val(viewBoxArr(1))
		      viewBoxWidth = Val(viewBoxArr(2))
		      viewBoxHeight = Val(viewBoxArr(3))
		      
		      if (viewBoxWidth > 0) and (viewBoxHeight > 0) then
		        preserveAspectRatio = node.GetAttribute("preserveAspectRatio").Trim().Lowercase()
		        if preserveAspectRatio = "" then
		          preserveAspectRatio = "xmidymid meet"
		        end if
		        
		        if preserveAspectRatio = "none" then
		          scaleX = width / viewBoxWidth
		          scaleY = height / viewBoxHeight
		          
		          mulMatrix = scaleMatrix(scaleX, scaleY)
		          matrix = matrixMultiply(matrix, mulMatrix)
		        else
		          align = preserveAspectRatio
		          meetOrSlice = "meet"
		          if align.IndexOf(" ") >= 0 then
		            meetOrSlice = align.NthField(" ", 2).Trim()
		            align = align.NthField(" ", 1).Trim()
		          end if
		          
		          scaleX = width / viewBoxWidth
		          scaleY = height / viewBoxHeight
		          if meetOrSlice = "slice" then
		            scale = Max(scaleX, scaleY)
		          else
		            scale = Min(scaleX, scaleY)
		          end if
		          
		          xOffset = 0
		          yOffset = 0
		          
		          if align.IndexOf("xmid") >= 0 then
		            xOffset = (width - (viewBoxWidth * scale)) / 2.0
		          elseif align.IndexOf("xmax") >= 0 then
		            xOffset = width - (viewBoxWidth * scale)
		          end if
		          
		          if align.IndexOf("ymid") >= 0 then
		            yOffset = (height - (viewBoxHeight * scale)) / 2.0
		          elseif align.IndexOf("ymax") >= 0 then
		            yOffset = height - (viewBoxHeight * scale)
		          end if
		          
		          mulMatrix = translationMatrix(xOffset, yOffset)
		          matrix = matrixMultiply(matrix, mulMatrix)
		          
		          mulMatrix = scaleMatrix(scale, scale)
		          matrix = matrixMultiply(matrix, mulMatrix)
		        end if
		        
		        mulMatrix = translationMatrix(-minX, -minY)
		        matrix = matrixMultiply(matrix, mulMatrix)
		      end if
		    end if
		  end if
		  
		  i = 0
		  while i < node.ChildCount
		    renderNode node.Child(i), g, matrix, style
		    i = i + 1
		  wend
		  
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub render_text(node As XmlNode, g As Graphics, parentMatrix() As Double, parentStyle As JSONItem)
		  Var localStyle As JSONItem
		  Var style As JSONItem
		  Var elementStyle As JSONItem
		  Var matrix() As Double
		  Var mulMatrix() As Double
		  Var elementMatrix() As Double
		  Var element As Picture
		  Var eg As Graphics
		  Var tspanStyle As JSONItem
		  Var textStr As String
		  Var x As Double
		  Var y As Double
		  Var penX As Double
		  Var penY As Double
		  Var hasDrawnText As Boolean
		  Var childNode As XmlNode
		  Var j As Integer
		  Var elementFill As String
		  Var advanceWidth As Double
		  Var fill As String
		  Var strShape as new TextShape
		  Var i As Integer
		  
		  style = new JSONItem("{}")
		  style.ApplyValues parentStyle
		  localStyle = buildStyleItem(node)
		  style.ApplyValues localStyle
		  matrix = buildTransformationMatrix(localStyle.Lookup("transform", ""))
		  matrix = matrixMultiply(parentMatrix, matrix)
		  
		  x = Val(style.LookupString("x", "0"))
		  y = Val(style.LookupString("y", "0"))
		  penX = x
		  penY = y
		  hasDrawnText = false
		  fill = style.LookupString("fill", "#000000")
		  if (fill <> "none") and style.HasKey("fill-opacity") then
		    if Val(style.Value("fill-opacity")) = 0 then
		      fill = "none"
		    elseif Val(style.Value("fill-opacity")) = 1 then
		      // do nothing
		    else
		      'break // todo
		    end if
		  end if
		  
		  i = 0
		  while i < node.ChildCount
		    
		    textStr = ""
		    childNode = node.Child(i)
		    
		    elementStyle = new JSONItem(style.ToString())
		    
		    if childNode.Name = "#text" then
		      textStr = childNode.Value
		    elseif childNode.Name = "tspan" then
		      
		      tspanStyle = buildStyleItem(childNode)
		      elementStyle.ApplyValues(tspanStyle)
		      j = 0
		      while j < childNode.ChildCount
		        if childNode.Child(j).Name = "#text" then
		          textStr = textStr + childNode.Child(j).Value
		        end if
		        j = j + 1
		      wend
		      textStr = textStr.Trim()
		      
		      if tspanStyle.HasKey("x") then
		        penX = Val(elementStyle.LookupString("x", Str(penX)))
		      end if
		      if tspanStyle.HasKey("y") then
		        penY = Val(elementStyle.LookupString("y", Str(penY)))
		      end if
		      if tspanStyle.HasKey("dx") then
		        penX = penX + Val(elementStyle.LookupString("dx", "0"))
		      end if
		      if tspanStyle.HasKey("dy") then
		        penY = penY + Val(elementStyle.LookupString("dy", "0"))
		      end if
		      
		    end if
		    
		    textStr = textStr.ReplaceLineEndings(" ")
		    textStr = textStr.ReplaceAll(Chr(9), " ")
		    textStr = collapseSpaces(textStr)
		    if not hasDrawnText then
		      while (textStr.Length > 0) and (textStr.Left(1) = " ")
		        textStr = textStr.Middle(1)
		      wend
		    end if
		    
		    if textStr <> "" then
		      
		      g.FontName = elementStyle.LookupString("font-family", "Arial")
		      g.FontUnit = FontUnits.Pixel
		      g.FontSize = Val(elementStyle.LookupString("font-size", "16"))
		      if g.FontSize <= 0 then
		        g.FontSize = 16
		      end if
		      g.Bold = false
		      if elementStyle.LookupString("font-weight", "") = "bold" then
		        g.Bold = true
		      end if
		      g.Italic = false
		      if elementStyle.LookupString("font-style", "") = "italic" then
		        g.Italic = true
		      end if
		      
		      advanceWidth = g.TextWidth(textStr)
		      elementFill = elementStyle.LookupString("fill", fill)
		      if (elementFill <> "none") and elementStyle.HasKey("fill-opacity") then
		        if Val(elementStyle.Value("fill-opacity")) = 0 then
		          elementFill = "none"
		        end if
		      end if
		      
		      mulMatrix = translationMatrix(penX, penY)
		      elementMatrix = matrixMultiply(matrix, mulMatrix)
		      
		      if elementFill <> "none" then
		        strShape.FillColor = determineColor(elementFill)
		        strShape.FontName = g.FontName
		        strShape.FontUnit = g.FontUnit
		        strShape.FontSize = g.FontSize * elementMatrix(0)
		        strShape.Bold = g.Bold
		        strShape.Italic = g.Italic
		        select case elementStyle.Lookup("text-anchor", "start")
		        case "end"
		          strShape.HorizontalAlignment = TextShape.Alignment.Right
		        case "middle"
		          strShape.HorizontalAlignment = TextShape.Alignment.Left
		          mulMatrix = translationMatrix(-advanceWidth / 2, 0)
		          elementMatrix = matrixMultiply(elementMatrix, mulMatrix)
		        case else
		          strShape.HorizontalAlignment = TextShape.Alignment.Left
		        end select
		        strShape.VerticalAlignment = TextShape.Alignment.BaseLine
		        strShape.Text = textStr
		        
		        // to speed up rendering and improve quality, we only use DrawTransformedPicture when needed
		        
		        if (elementMatrix(1) = 0) and (elementMatrix(3) = 0)  and (elementMatrix(6) = 0) and _
		          (elementMatrix(7) = 0) and (elementMatrix(8) = 1) and _
		          (elementMatrix(0) = elementMatrix(4)) then
		          
		          g.DrawObject strShape, elementMatrix(2), elementMatrix(5)
		          
		        else
		          element = new Picture(Max(1, advanceWidth), Max(1, g.TextHeight))
		          eg = element.Graphics
		          
		          eg.DrawObject strShape, _
		          0, _
		          0
		          
		          g.DrawTransformedPicture element, elementMatrix
		        end if
		      end if
		      
		      penX = penX + advanceWidth
		      hasDrawnText = true
		      
		    end if
		    
		    i = i + 1
		    
		  wend
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Function rotationMatrix(angle As Double) As Double()
		  Var result() As Double = Array( _
		  cos(angle * DegToRad), -sin(angle * DegToRad), 0.0, _
		  sin(angle * DegToRad), cos(angle * DegToRad), 0.0, _
		  0.0, 0.0, 1.0)
		  
		  return result
		  
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Function scaleMatrix(sx As Double, sy As Double) As Double()
		  Var result() As Double = Array( _
		  sx, 0.0, 0.0, _
		  0.0, sy, 0.0, _
		  0.0, 0.0, 1.0)
		  
		  return result
		  
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Function skewXMatrix(angle As Double) As Double()
		  Var result() As Double = Array( _
		  1.0, tan(angle * DegToRad), 0.0, _
		  0.0, 1.0, 0.0, _
		  0.0, 0.0, 1.0)
		  
		  return result
		  
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Function skewYMatrix(angle As Double) As Double()
		  Var result() As Double = Array( _
		  1.0, 0.0, 0.0, _
		  tan(angle * DegToRad), 1.0, 0.0, _
		  0.0, 0.0, 1.0)
		  
		  return result
		  
		End Function
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub transformPoint(ByRef x As Double, ByRef y As Double, matrix() As Double)
		  Var cx As Double
		  Var cy As Double
		  Var cw As Double
		  
		  cx = matrix(0) * x + matrix(1) * y + matrix(2)
		  cy = matrix(3) * x + matrix(4) * y + matrix(5)
		  cw = matrix(6) * x + matrix(7) * y + matrix(8)
		  
		  x = (cx / cw)
		  y = (cy / cw)
		  
		  
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Sub transformPoint(ByRef x As Integer, ByRef y As Integer, matrix() As Double)
		  Var cx As Double
		  Var cy As Double
		  Var cw As Double
		  
		  cx = matrix(0) * x + matrix(1) * y + matrix(2)
		  cy = matrix(3) * x + matrix(4) * y + matrix(5)
		  cw = matrix(6) * x + matrix(7) * y + matrix(8)
		  
		  x = (cx / cw)
		  y = (cy / cw)
		  
		  
		End Sub
	#tag EndMethod

	#tag Method, Flags = &h21
		Private Function translationMatrix(tx As Double, ty As Double) As Double()
		  Var result() As Double = Array( _
		  1.0, 0.0, tx, _
		  0.0, 1.0, ty, _
		  0.0, 0.0, 1.0)
		  
		  return result
		  
		End Function
	#tag EndMethod


	#tag Property, Flags = &h21
		Private mClasses As JSONItem
	#tag EndProperty

	#tag Property, Flags = &h21
		Private mNodes As Dictionary
	#tag EndProperty


	#tag Constant, Name = DegToRad, Type = Double, Dynamic = False, Default = \"0.0174533", Scope = Private
	#tag EndConstant

	#tag Constant, Name = Pi, Type = Double, Dynamic = False, Default = \"3.1415927", Scope = Private
	#tag EndConstant

	#tag Constant, Name = RadToDeg, Type = Double, Dynamic = False, Default = \"57.2958", Scope = Private
	#tag EndConstant


	#tag Enum, Name = SVGErrorEnum, Type = Integer, Flags = &h0
		MalformedXML=1
		  ExpectedPathCommand = 2
		  NodeNotFound = 3
		UnknownElement = 4
	#tag EndEnum


	#tag ViewBehavior
		#tag ViewProperty
			Name="Name"
			Visible=true
			Group="ID"
			InitialValue=""
			Type="String"
			EditorType=""
		#tag EndViewProperty
		#tag ViewProperty
			Name="Index"
			Visible=true
			Group="ID"
			InitialValue="-2147483648"
			Type="Integer"
			EditorType=""
		#tag EndViewProperty
		#tag ViewProperty
			Name="Super"
			Visible=true
			Group="ID"
			InitialValue=""
			Type="String"
			EditorType=""
		#tag EndViewProperty
		#tag ViewProperty
			Name="Left"
			Visible=true
			Group="Position"
			InitialValue="0"
			Type="Integer"
			EditorType=""
		#tag EndViewProperty
		#tag ViewProperty
			Name="Top"
			Visible=true
			Group="Position"
			InitialValue="0"
			Type="Integer"
			EditorType=""
		#tag EndViewProperty
	#tag EndViewBehavior
End Module
#tag EndModule
